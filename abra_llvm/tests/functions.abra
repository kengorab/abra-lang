// Default-valued parameters
func foo(a: Int, b = bar(a), c = bar(a, b), arr = [a, b, c]): Int = a + b + c + arr.length
func bar(x: Int, y = 10): Int = x + y
/// Expect: 31
println(foo(2))

// Variadic parameters
func variadic(*items: Int[]) = println(items)
/// Expect: [1, 2, 3]
variadic(1, 2, 3)
/// Expect: []
variadic()

// Function as value
func callFn(fn: (Int) => Int) = println(fn(16))
func callFn2(fn: (Int, Int, Int) => Int) = println(fn(16, 17, 18))
func callFn3(fn: (Int, Int, Int, Int) => Int) = println(fn(16, 17, 18, 19))

func abc(): Int = 24
val abcVal = abc
/// Expect: 24
println(abc())

func f0(x: Int): Int = x
/// Expect: 16
callFn(f0)

func f1(x = 12): Int = x
/// Expect: 16
callFn(f1)

func f2(x: Int, y = 6): Int = x + y
/// Expect: 22
callFn(f2)

func f3(x = 12, y = 6): Int = x + y
/// Expect: 22
callFn(f3)

func f4(x: Int): Int = x
/// Expect: 16
callFn2(f4)

func f5(x: Int, y: Int): Int = x + y
/// Expect: 33
callFn2(f5)

func f6(x: Int, y = 12): Int = x + y
/// Expect: 33
callFn2(f6)

func f7(x: Int, y = 12, z = 100): Int = x + y + z
/// Expect: 51
callFn3(f7)

// Closures
var capturedFloat = 1.1
func closure1(one: Int): Float {
  capturedFloat += 1.1
  val x = capturedFloat + one
  x
}
/// Expect: 1.1
println(capturedFloat)
/// Expect: 3.2
println(closure1(one: 1))
/// Expect: 2.2
println(capturedFloat)

capturedFloat = 10.1
/// Expect: 10.1
println(capturedFloat)
/// Expect: 12.2
println(closure1(one: 1))
/// Expect: 11.2
println(capturedFloat)

val capturedArray = [1, 2, 3]
func closure2(zero = 0): Int {
  capturedArray.pop()
  capturedArray.length + zero
}
/// Expect: [1, 2, 3]
println(capturedArray)
/// Expect: 2
println(closure2())
/// Expect: [1, 2]
println(capturedArray)
capturedArray.push(3)
/// Expect: [1, 2, 3]
println(capturedArray)

var capturedInt = 1
func closure3<T>(arr: T[], extra = 0) {
  capturedInt += arr.length + extra
}

/// Expect: 1
println(capturedInt)
closure3([1, 2, 3])
/// Expect: 4
println(capturedInt)
capturedInt = 1
closure3(["a", "b"], 12)
/// Expect: 15
println(capturedInt)

capturedInt = 1
func closure4() { capturedInt += 2 }
func containsClosures1() { closure4() }
func containsClosures2() { containsClosures1() }

/// Expect: 1
println(capturedInt)
containsClosures2()
/// Expect: 3
println(capturedInt)
containsClosures2()
/// Expect: 5
println(capturedInt)
