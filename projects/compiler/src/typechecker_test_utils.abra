import LiteralAstNode, IndexingMode from "./parser"
import Type, TypeKind, TypedModule, TypedAstNode, TypedAstNodeKind, Variable, Function, FunctionKind, Scope, Struct, Enum, InstanceKind, AccessorPathSegment, TypedInvokee, TypedIndexingNode, TypedAssignmentMode, Field, EnumVariantKind, Export, TypedMatchCaseKind, TypedForIterKind from "./typechecker"
import printTokenAsJson, printLabelAsJson, printBindingPatternAsJson from "./test_utils"

pub type Jsonifier {
  pub allowPlaceholderNodes: Bool = false
  indentLevel: Int = 0

  pub func printModules(self, modules: TypedModule[]) {
    val manyModules = modules.length > 1

    if manyModules {
      stdoutWriteln("[")
      self.indentInc()
    }

    for mod, idx in modules {
      self.indent()
      self.printModule(mod)
      if manyModules {
        if idx != modules.length - 1 {
          stdoutWrite(",")
        }
        stdoutWriteln()
      }
    }

    if manyModules {
      self.indentDec()
      stdoutWrite("]")
    }

    stdoutWriteln()
  }

  func printModule(self, mod: TypedModule) {
    stdoutWriteln("{")
    self.indentInc()

    self.println("\"id\": ${mod.id},")
    self.println("\"name\": \"${mod.name}\",")

    if !mod.exports.isEmpty() {
      self.println("\"exports\": [")
      self.indentInc()

      for (name, exp), idx in mod.exports {
        self.println("{")
        self.indentInc()

        match exp {
          Export.Variable => self.println("\"kind\": \"variable\",")
          Export.Function => self.println("\"kind\": \"function\",")
          Export.Type => self.println("\"kind\": \"type\",")
        }
        self.println("\"name\": \"$name\"")

        self.indentDec()
        self.print("}")

        val comma = if idx != mod.exports.size - 1 "," else ""
        stdoutWriteln(comma)
      }
      self.indentDec()
      self.println("],")
    }

    self.print("\"code\": ")
    self.printNodes(mod.code)
    stdoutWriteln()

    self.indentDec()
    self.print("}")
  }

  func printNodes(self, nodes: TypedAstNode[]) {
    stdoutWriteln("[")
    self.indentInc()

    for node, idx in nodes {
      self.indent()
      self.printNode(node)
      val comma = if idx != nodes.length - 1 "," else ""
      stdoutWriteln(comma)
    }

    self.indentDec()
    self.print("]")
  }

  func printType(self, ty: Type) {
    stdoutWriteln("{")
    self.indentInc()

    match ty.kind {
      TypeKind.CouldNotDetermine => {
        self.println("\"kind\": \"could not determine\"")
      }
      TypeKind.PrimitiveUnit => {
        self.println("\"kind\": \"primitive\",")
        self.println("\"primitive\": \"Unit\"")
      }
      TypeKind.PrimitiveInt => {
        self.println("\"kind\": \"primitive\",")
        self.println("\"primitive\": \"Int\"")
      }
      TypeKind.PrimitiveFloat => {
        self.println("\"kind\": \"primitive\",")
        self.println("\"primitive\": \"Float\"")
      }
      TypeKind.PrimitiveBool => {
        self.println("\"kind\": \"primitive\",")
        self.println("\"primitive\": \"Bool\"")
      }
      TypeKind.PrimitiveString => {
        self.println("\"kind\": \"primitive\",")
        self.println("\"primitive\": \"String\"")
      }
      TypeKind.Never => {
        self.println("\"kind\": \"never\"")
      }
      TypeKind.Generic(name) => {
        self.println("\"kind\": \"generic\",")
        self.println("\"name\": \"$name\"")
      }
      TypeKind.Instance(instanceKind, generics) => {
        match instanceKind {
          InstanceKind.Struct(struct) => {
            self.println("\"kind\": \"instance\",")
            self.println("\"struct\": { \"moduleId\": ${struct.moduleId}, \"name\": \"${struct.label.name}\" },")
          }
          InstanceKind.Enum(enum_) => {
            self.println("\"kind\": \"enumInstance\",")
            self.println("\"enum\": { \"moduleId\": ${enum_.moduleId}, \"name\": \"${enum_.label.name}\" },")
          }
        }
        self.print("\"typeParams\": ")
        self.array(generics, t => self.printType(t))
        stdoutWriteln()
      }
      TypeKind.Tuple(types) => {
        self.println("\"kind\": \"tuple\",")
        self.print("\"types\": ")
        self.array(types, t => self.printType(t))
        stdoutWriteln()
      }
      TypeKind.Func(params, ret) => {
        self.println("\"kind\": \"function\",")
        self.print("\"parameters\": ")
        self.array(params, p => {
          stdoutWriteln("{")
          self.indentInc()

          self.println("\"required\": ${p[1]},")

          self.print("\"type\": ")
          self.printType(p[0])
          stdoutWriteln()

          self.indentDec()
          self.print("}")
        })
        stdoutWriteln(",")
        self.print("\"returnType\": ")
        self.printType(ret)
        stdoutWriteln()
      }
      TypeKind.Type(instanceKind) => {
        match instanceKind {
          InstanceKind.Struct(struct) => {
            self.println("\"kind\": \"struct\",")
            self.print("\"struct\": ")
            self.printStruct(struct: struct, abridged: true)
          }
          InstanceKind.Enum(enum_) => {
            self.println("\"kind\": \"enum\",")
            self.print("\"enum\": ")
            self.printEnum(enum_: enum_, abridged: true)
          }
        }
        stdoutWriteln()
      }
      TypeKind.Hole => self.println("\"kind\": \"hole\"")
    }

    self.indentDec()
    self.print("}")
  }

  func printStruct(self, struct: Struct, abridged: Bool) {
    if abridged {
      stdoutWrite("{ \"moduleId\": ${struct.moduleId}, \"name\": \"${struct.label.name}\" }")
      return
    }

    stdoutWriteln("{")
    self.indentInc()

    self.println("\"moduleId\": ${struct.moduleId},")

    self.print("\"name\": ")
    printLabelAsJson(struct.label)
    stdoutWriteln(",")

    self.print("\"typeParams\": ")
    self.array(struct.typeParams, p => stdoutWrite("\"$p\""))
    stdoutWriteln(",")

    self.print("\"fields\": ")
    self.array(struct.fields, f => self.printField(f))
    stdoutWriteln(",")

    self.print("\"instanceMethods\": ")
    self.array(struct.instanceMethods, m => self.printFunc(m))
    stdoutWriteln(",")

    self.print("\"staticMethods\": ")
    self.array(struct.staticMethods, m => self.printFunc(m))
    stdoutWriteln()

    self.indentDec()
    self.print("}")
  }

  func printField(self, field: Field) {
    stdoutWriteln("{")
    self.indentInc()

    self.print("\"name\": ")
    printLabelAsJson(field.name)
    stdoutWriteln(",")

    self.print("\"type\": ")
    self.printType(field.ty)
    stdoutWriteln(",")

    self.print("\"initializer\": ")
    self.opt(field.initializer, n => self.printNode(n))

    if field.isPublic {
      stdoutWriteln(",")
      self.print("\"isPublic\": true")
    }
    stdoutWriteln()

    self.indentDec()
    self.print("}")
  }

  func printEnum(self, enum_: Enum, abridged: Bool) {
    if abridged {
      stdoutWrite("{ \"moduleId\": ${enum_.moduleId}, \"name\": \"${enum_.label.name}\" }")
      return
    }

    stdoutWriteln("{")
    self.indentInc()

    self.println("\"moduleId\": ${enum_.moduleId},")

    self.print("\"name\": ")
    printLabelAsJson(enum_.label)
    stdoutWriteln(",")

    self.print("\"typeParams\": ")
    self.array(enum_.typeParams, p => stdoutWrite("\"$p\""))
    stdoutWriteln(",")

    self.print("\"variants\": ")
    self.array(enum_.variants, v => {
      stdoutWriteln("{")
      self.indentInc()

      self.print("\"label\": ")
      printLabelAsJson(v.label)
      stdoutWriteln(",")

      match v.kind {
        EnumVariantKind.Constant => {
          self.println("\"kind\": \"constant\"")
        }
        EnumVariantKind.Container(fields) => {
          self.println("\"kind\": \"container\",")
          self.print("\"fields\": ")
          self.array(fields, f => self.printField(f))
          stdoutWriteln()
        }
      }

      self.indentDec()
      self.print("}")
    })
    stdoutWriteln(",")

    self.print("\"instanceMethods\": ")
    self.array(enum_.instanceMethods, m => self.printFunc(m))
    stdoutWriteln(",")

    self.print("\"staticMethods\": ")
    self.array(enum_.staticMethods, m => self.printFunc(m))
    stdoutWriteln()

    self.indentDec()
    self.print("}")
  }

  func printNode(self, node: TypedAstNode) {
    stdoutWriteln("{")
    self.indentInc()

    self.print("\"token\": ")
    printTokenAsJson(node.token, 0, self.indentLevel)
    stdoutWriteln(",")

    self.print("\"type\": ")
    self.printType(node.ty)
    stdoutWriteln(",")

    self.print("\"node\": ")
    self.printNodeKind(node.kind)
    stdoutWriteln()

    self.indentDec()
    self.print("}")
  }

  func printNodeKind(self, kind: TypedAstNodeKind) {
    stdoutWriteln("{")
    self.indentInc()

    match kind {
      TypedAstNodeKind.Literal(value) => {
        self.println("\"kind\": \"literal\",")
        match value {
          LiteralAstNode.Int(value) => self.println("\"value\": $value")
          LiteralAstNode.Float(value) => self.println("\"value\": $value")
          LiteralAstNode.Bool(value) => self.println("\"value\": $value")
          LiteralAstNode.Char(value) => self.println("\"value\": \"${value.hex()}\"")
          LiteralAstNode.String(value) => self.println("\"value\": \"$value\"")
        }
      }
      TypedAstNodeKind.StringInterpolation(exprs) => {
        self.println("\"kind\": \"stringInterpolation\",")

        self.print("\"exprs\": ")
        self.array(exprs, n => self.printNode(n))
        stdoutWriteln()
      }
      TypedAstNodeKind.Unary(op, expr) => {
        self.println("\"kind\": \"unary\",")
        self.println("\"op\": \"$op\",")
        self.print("\"expr\": ")
        self.printNode(expr)
        stdoutWriteln()
      }
      TypedAstNodeKind.Binary(left, op, right) => {
        self.println("\"kind\": \"binary\",")
        self.println("\"op\": \"$op\",")
        self.print("\"left\": ")
        self.printNode(left)
        stdoutWriteln(",")
        self.print("\"right\": ")
        self.printNode(right)
        stdoutWriteln()
      }
      TypedAstNodeKind.Grouped(inner) => {
        self.println("\"kind\": \"grouped\",")
        self.print("\"expr\": ")
        self.printNode(inner)
        stdoutWriteln()
      }
      TypedAstNodeKind.Identifier(name, _, _, _) => {
        self.println("\"kind\": \"identifier\",")

        self.print("\"name\": \"$name\"")
        stdoutWriteln()
      }
      TypedAstNodeKind.Accessor(head, middle, tail) => {
        self.println("\"kind\": \"accessor\",")

        self.print("\"head\": ")
        self.printNode(head)
        stdoutWriteln(",")

        self.print("\"middle\": ")
        self.array(middle, seg => {
          stdoutWriteln("{")
          self.indentInc()

          match seg {
            AccessorPathSegment.EnumVariant(_, _, enum_, variant) => {
              self.println("\"kind\": \"enumVariant\",")
              self.println("\"name\": \"${enum_.label.name}.${variant.label.name}\"")
            }
            AccessorPathSegment.Method(_, f, _, _) => {
              self.println("\"kind\": \"method\",")
              self.println("\"name\": \"${f.label.name}\"")
            }
            AccessorPathSegment.Field(_, ty, f, _) => {
              self.println("\"kind\": \"field\",")
              self.println("\"name\": \"${f.name.name}\",")
              self.print("\"type\": ")
              self.printType(ty)
              stdoutWriteln()
            }
          }

          self.indentDec()
          self.print("}")
        })
        stdoutWriteln(",")

        self.println("\"tail\": {")
        self.indentInc()
        match tail {
          AccessorPathSegment.EnumVariant(_, _, enum_, variant) => {
            self.println("\"kind\": \"enumVariant\",")
            self.println("\"name\": \"${enum_.label.name}.${variant.label.name}\"")
          }
          AccessorPathSegment.Method(_, f, _, _) => {
            self.println("\"kind\": \"method\",")
            self.println("\"name\": \"${f.label.name}\"")
          }
          AccessorPathSegment.Field(_, ty, f, _) => {
            self.println("\"kind\": \"field\",")
            self.println("\"name\": \"${f.name.name}\",")
            self.print("\"type\": ")
            self.printType(ty)
            stdoutWriteln()
          }
        }
        self.indentDec()
        self.println("}")
      }
      TypedAstNodeKind.Invocation(invokee, arguments, _) => {
        self.println("\"kind\": \"invocation\",")

        self.print("\"invokee\": ")
        match invokee {
          TypedInvokee.Method(fn, selfVal, isOptSafe) => {
            stdoutWriteln("{")
            self.indentInc()

            self.println("\"method\": \"${fn.label.name}\",")

            self.print("\"type\": ")
            self.printType(fn.getType())
            stdoutWriteln(",")

            self.println("\"isOptSafe\": $isOptSafe,")
            self.print("\"self\": ")
            self.printNode(selfVal)
            stdoutWriteln()

            self.indentDec()
            self.print("}")
          }
          TypedInvokee.Function(fn) => {
            stdoutWriteln("{")
            self.indentInc()

            self.println("\"function\": \"${fn.label.name}\",")

            self.print("\"type\": ")
            self.printType(fn.getType())
            stdoutWriteln()

            self.indentDec()
            self.print("}")
          }
          TypedInvokee.Struct(struct) => self.printStruct(struct: struct, abridged: true)
          TypedInvokee.Expr(expr) => self.printNode(expr)
          TypedInvokee.EnumVariant(enum_, variant) => stdoutWrite("\"${enum_.label.name}.${variant.label.name}\"")
        }
        stdoutWriteln(",")

        self.print("\"arguments\": ")
        self.array(arguments, n => self.opt(n, n => self.printNode(n)))
        stdoutWriteln()
      }
      TypedAstNodeKind.Array(items) => {
        self.println("\"kind\": \"array\",")

        self.print("\"items\": ")
        self.array(items, n => self.printNode(n))
        stdoutWriteln()
      }
      TypedAstNodeKind.Set(items) => {
        self.println("\"kind\": \"set\",")

        self.print("\"items\": ")
        self.array(items, n => self.printNode(n))
        stdoutWriteln()
      }
      TypedAstNodeKind.Map(pairs) => {
        self.println("\"kind\": \"map\",")

        self.print("\"pairs\": ")
        self.array(pairs, p => {
          self.array([p[0], p[1]], n => self.printNode(n))
        })
        stdoutWriteln()
      }
      TypedAstNodeKind.Tuple(items) => {
        self.println("\"kind\": \"tuple\",")

        self.print("\"items\": ")
        self.array(items, n => self.printNode(n))
        stdoutWriteln()
      }
      TypedAstNodeKind.Indexing(node) => {
        self.println("\"kind\": \"indexing\",")

        self.print("\"node\": ")
        self.printTypedIndexingNode(node)
        stdoutWriteln()
      }
      TypedAstNodeKind.Lambda(fn, _) => {
        self.println("\"kind\": \"lambda\",")
        self.print("\"function\": ")
        self.printFunc(fn)
        stdoutWriteln()
      }
      TypedAstNodeKind.Assignment(mode, op, expr) => {
        self.println("\"kind\": \"assignment\",")

        self.print("\"expr\": ")
        self.printNode(expr)
        stdoutWriteln(",")

        self.println("\"op\": \"$op\",")

        self.print("\"mode\": ")

        match mode {
          TypedAssignmentMode.Variable(v) => {
            stdoutWriteln("{")
            self.indentInc()

            self.println("\"kind\": \"variable\",")
            self.print("\"variable\": ")
            self.printVariable(v)
            stdoutWriteln()

            self.indentDec()
            self.print("}")
            stdoutWriteln()
          }
          TypedAssignmentMode.Indexing(typedIndexingNode) => {
            stdoutWriteln("{")
            self.indentInc()

            self.println("\"kind\": \"indexing\",")
            self.print("\"indexNode\": ")
            self.printTypedIndexingNode(typedIndexingNode)
            stdoutWriteln()

            self.indentDec()
            self.print("}")
            stdoutWriteln()
          }
          TypedAssignmentMode.Accessor(head, mid, tail) => {
            self.printNodeKind(TypedAstNodeKind.Accessor(head, mid, tail))
            stdoutWriteln()
          }
        }
      }
      TypedAstNodeKind.If(isStatement, condition, conditionBindingPattern, ifBlock, _, elseBlock, _) => {
        self.println("\"kind\": \"if\",")

        self.println("\"isStatement\": $isStatement,")

        self.print("\"condition\": ")
        self.printNode(condition)
        stdoutWriteln(",")

        self.print("\"conditionBindingPattern\": ")
        self.opt(conditionBindingPattern, pair => {
          stdoutWriteln("{")
          self.indentInc()

          self.print("\"bindingPattern\": ")
          printBindingPatternAsJson(pair[0], 0, self.indentLevel)
          stdoutWriteln(",")

          self.print("\"variables\": ")
          self.array(pair[1], v => self.printVariable(v))
          stdoutWriteln()

          self.indentDec()
          self.print("}")
        })
        stdoutWriteln(",")

        self.print("\"ifBlock\": ")
        self.array(ifBlock, n => self.printNode(n))
        stdoutWriteln(",")

        self.print("\"elseBlock\": ")
        self.array(elseBlock, n => self.printNode(n))
        stdoutWriteln()
      }

      TypedAstNodeKind.Match(isStatement, subject, cases) => {
        self.println("\"kind\": \"match\",")

        self.println("\"isStatement\": $isStatement,")

        self.print("\"subject\": ")
        self.printNode(subject)
        stdoutWriteln(",")

        self.print("\"cases\": ")
        self.array(cases, case => {
          stdoutWriteln("{")
          self.indentInc()

          self.print("\"kind\": ")
          match case.kind {
            TypedMatchCaseKind.Type(ty) => {
              stdoutWriteln("{")
              self.indentInc()

              self.println("\"kind\": \"type\",")
              self.print("\"type\": ")
              self.printType(ty)
              stdoutWriteln()

              self.indentDec()
              self.print("}")
            }
            TypedMatchCaseKind.EnumVariant(enum_, _, variantIdx, destructuredVariables) => {
              stdoutWriteln("{")
              self.indentInc()

              self.println("\"kind\": \"enumVariant\",")
              self.println("\"variantIdx\": $variantIdx,")
              self.print("\"enum\": ")
              self.printEnum(enum_: enum_, abridged: true)
              stdoutWriteln(",")
              self.print("\"destructuredVariables\": ")
              self.array(destructuredVariables, v => self.printVariable(v))
              stdoutWriteln()

              self.indentDec()
              self.print("}")
            }
            TypedMatchCaseKind.Literal(value) => {
              stdoutWriteln("{")
              self.indentInc()

              self.println("\"kind\": \"literal\",")
              match value {
                LiteralAstNode.Int(value) => self.println("\"value\": $value")
                LiteralAstNode.Float(value) => self.println("\"value\": $value")
                LiteralAstNode.Bool(value) => self.println("\"value\": $value")
                LiteralAstNode.String(value) => self.println("\"value\": \"$value\"")
              }

              self.indentDec()
              self.print("}")
            }
            TypedMatchCaseKind.None_ => stdoutWrite("\"None\"")
            TypedMatchCaseKind.Else => stdoutWrite("\"else\"")
          }
          stdoutWriteln(",")

          self.print("\"binding\": ")
          self.opt(case.binding, v => self.printVariable(v))
          stdoutWriteln(",")

          self.print("\"body\": ")
          self.array(case.body, n => self.printNode(n))
          stdoutWriteln()

          self.indentDec()
          self.print("}")
        })
        stdoutWriteln()
      }
      TypedAstNodeKind.Try(expr, elseClause) => {
        self.println("\"kind\": \"try\",")

        self.print("\"expr\": ")
        self.printNode(expr)
        if elseClause |elseClause| {
          stdoutWriteln(",")
          self.print("\"elseBindingPattern\": ")
          self.opt(elseClause.pattern, _pair => {
            val (pat, vars) = _pair
            stdoutWriteln("{")
            self.indentInc()

            self.print("\"bindingPattern\": ")
            printBindingPatternAsJson(pat, 0, self.indentLevel)
            stdoutWriteln(",")

            self.print("\"variables\": ")
            self.array(vars, v => self.printVariable(v))
            stdoutWriteln()

            self.indentDec()
            self.print("}")
          })

          stdoutWriteln(",")

          self.print("\"elseBlock\": ")
          self.array(elseClause.block, n => self.printNode(n))
        }
        stdoutWriteln()
      }

      // Statements
      TypedAstNodeKind.While(condition, conditionBindingPattern, block, _) => {
        self.println("\"kind\": \"while\",")

        self.print("\"condition\": ")
        self.printNode(condition)
        stdoutWriteln(",")

        self.print("\"conditionBindingPattern\": ")
        self.opt(conditionBindingPattern, _pair => {
          val (pat, vars) = _pair
          stdoutWriteln("{")
          self.indentInc()

          self.print("\"bindingPattern\": ")
          printBindingPatternAsJson(pat, 0, self.indentLevel)
          stdoutWriteln(",")

          self.print("\"variables\": ")
          self.array(vars, v => self.printVariable(v))
          stdoutWriteln()

          self.indentDec()
          self.print("}")
        })
        stdoutWriteln(",")

        self.print("\"block\": ")
        self.array(block, n => self.printNode(n))
        stdoutWriteln()
      }
      TypedAstNodeKind.For(typedForIterKind, itemBindingPattern, indexBinding, block, _) => {
        self.println("\"kind\": \"for\",")

        self.print("\"iterator\": ")
        match typedForIterKind {
          TypedForIterKind.Expr(iterator) => self.printNode(iterator)
          TypedForIterKind.Range(start, end) => {
            stdoutWriteln("{")
            self.indentInc()
            self.print("\"start\": ")
            self.printNode(start)
            stdoutWriteln(",")
            self.print("\"end\": ")
            self.printNode(end)
            stdoutWriteln()
            // self.println(",")
            self.indentDec()
            self.print("}")
          }
        }
        stdoutWriteln(",")

        self.println("\"itemBindingPattern\": {")
        self.indentInc()
        self.print("\"bindingPattern\": ")
        printBindingPatternAsJson(itemBindingPattern[0], 0, self.indentLevel)
        stdoutWriteln(",")
        self.print("\"variables\": ")
        self.array(itemBindingPattern[1], v => self.printVariable(v))
        stdoutWriteln()
        self.indentDec()
        self.println("},")

        self.print("\"indexBinding\": ")
        self.opt(indexBinding, v => self.printVariable(v))
        stdoutWriteln(",")

        self.print("\"block\": ")
        self.array(block, n => self.printNode(n))
        stdoutWriteln()
      }
      TypedAstNodeKind.BindingDeclaration(node) => {
        self.println("\"kind\": \"bindingDeclaration\",")

        self.print("\"pattern\": ")
        printBindingPatternAsJson(node.bindingPattern, 0, self.indentLevel)
        stdoutWriteln(",")

        self.print("\"variables\": ")
        self.array(node.variables, v => self.printVariable(v))
        stdoutWriteln(",")

        self.print("\"expr\": ")
        self.opt(node.expr, n => self.printNode(n))
        stdoutWriteln()
      }
      TypedAstNodeKind.FunctionDeclaration(fn) => {
        self.println("\"kind\": \"functionDeclaration\",")
        self.print("\"function\": ")
        self.printFunc(fn)
        stdoutWriteln()
      }
      TypedAstNodeKind.TypeDeclaration(struct) => {
        self.println("\"kind\": \"typeDeclaration\",")
        self.print("\"struct\": ")
        self.printStruct(struct: struct, abridged: false)
        stdoutWriteln()
      }
      TypedAstNodeKind.EnumDeclaration(enum_) => {
        self.println("\"kind\": \"enumDeclaration\",")
        self.print("\"enum\": ")
        self.printEnum(enum_: enum_, abridged: false)
        stdoutWriteln()
      }
      TypedAstNodeKind.Break => {
        self.println("\"kind\": \"break\"")
      }
      TypedAstNodeKind.Continue => {
        self.println("\"kind\": \"continue\"")
      }
      TypedAstNodeKind.Return(expr) => {
        self.println("\"kind\": \"return\",")
        self.print("\"expr\": ")
        self.opt(expr, n => self.printNode(n))
        stdoutWriteln()
      }
      TypedAstNodeKind.Placeholder => {
        if self.allowPlaceholderNodes {
          self.println("\"kind\": \"placeholder\"")
        } else {
          self.println("ERROR: There should never be a Placeholder node emitted")
        }
      }
    }

    self.indentDec()
    self.print("}")
  }

  func printVariable(self, variable: Variable) {
    stdoutWriteln("{")
    self.indentInc()

    self.print("\"label\": ")
    printLabelAsJson(variable.label)
    stdoutWriteln(",")

    self.println("\"mutable\": ${variable.mutable},")

    if variable.isCaptured {
      self.println("\"isCaptured\": true,")
    }

    self.print("\"type\": ")
    self.printType(variable.ty)
    stdoutWriteln()

    self.indentDec()
    self.print("}")
  }

  func printFunc(self, function: Function) {
    stdoutWriteln("{")
    self.indentInc()

    self.print("\"label\": ")
    printLabelAsJson(function.label)
    stdoutWriteln(",")

    self.print("\"scope\": ")
    self.printScope(function.scope)
    stdoutWriteln(",")

    if function.isGenerated {
      self.println("\"isGenerated\": ${function.isGenerated},")
    }

    self.print("\"kind\": ")
    match function.kind {
      FunctionKind.Standalone => stdoutWrite("\"FunctionKind.Standalone\"")
      FunctionKind.InstanceMethod(_, isPublic) => {
        stdoutWrite("\"FunctionKind.InstanceMethod\"")
        stdoutWriteln(",")
        self.print("\"isPublic\": $isPublic")
      }
      FunctionKind.StaticMethod(_, isPublic) => {
        stdoutWrite("\"FunctionKind.StaticMethod\"")
        stdoutWriteln(",")
        self.print("\"isPublic\": $isPublic")
      }
    }
    stdoutWriteln(",")

    self.print("\"typeParameters\": ")
    self.array(function.typeParams, typeParam => {
      stdoutWriteln("{")
      self.indentInc()

      self.print("\"label\": ")
      printLabelAsJson(typeParam[1])
      stdoutWriteln(",")

      self.print("\"type\": ")
      self.printType(typeParam[0])
      stdoutWriteln()

      self.indentDec()
      self.print("}")
    })
    stdoutWriteln(",")

    self.print("\"parameters\": ")
    self.array(function.params, param => {
      stdoutWriteln("{")
      self.indentInc()

      self.print("\"label\": ")
      printLabelAsJson(param.label)
      stdoutWriteln(",")

      self.print("\"type\": ")
      self.printType(param.ty)
      stdoutWriteln(",")

      self.print("\"defaultValue\": ")
      self.opt(param.defaultValue, v => self.printNode(v))
      stdoutWriteln(",")

      self.println("\"isVariadic\": ${param.isVariadic}")

      self.indentDec()
      self.print("}")
    })
    stdoutWriteln(",")

    self.print("\"returnType\": ")
    self.printType(function.returnType)
    stdoutWriteln(",")

    if !function.captures.isEmpty() {
      self.print("\"captures\": ")
      self.array(function.captures, v => {
        self.printVariable(v)
      })
      stdoutWriteln(",")
    }

    if !function.capturedClosures.isEmpty() {
      self.print("\"capturedClosures\": ")
      self.array(function.capturedClosures, fn => {
        stdoutWrite("\"${fn.label.name}\"")
      })
      stdoutWriteln(",")
    }

    self.print("\"body\": ")
    self.array(function.body, n => self.printNode(n))
    stdoutWriteln()

    self.indentDec()
    self.print("}")
  }

  func printScope(self, scope: Scope) {
    stdoutWriteln("{")
    self.indentInc()

    self.println("\"name\": \"${scope.name}\",")

    self.print("\"variables\": ")
    self.array(scope.variables, v => self.printVariable(v))
    stdoutWriteln(",")

    self.print("\"functions\": ")
    self.array(scope.functions, f => self.printFunc(f))
    stdoutWriteln(",")

    self.print("\"structs\": ")
    self.array(scope.structs, s => self.printStruct(struct: s, abridged: true))
    stdoutWriteln(",")

    self.print("\"enums\": ")
    self.array(scope.enums, e => self.printEnum(enum_: e, abridged: true))
    stdoutWriteln(",")

    val generics = scope.generics.map(g => "\"$g\"")
    self.print("\"generics\": $generics")
    stdoutWriteln()

    self.indentDec()
    self.print("}")
  }

  func printTypedIndexingNode(self, node: TypedIndexingNode) {
    stdoutWriteln("{")
    self.indentInc()

    match node {
      TypedIndexingNode.ArrayLike(expr, indexingMode) => {
        self.println("\"kind\": \"arraylike\",")
        self.print("\"expr\": ")
        self.printNode(expr)
        stdoutWriteln(",")

        match indexingMode {
          IndexingMode.Single(idxNode) => {
            self.print("\"index\": ")
            self.printNode(idxNode)
            stdoutWriteln()
          }
          IndexingMode.Range(startNode, endNode) => {
            self.print("\"startIndex\": ")
            if startNode |n| self.printNode(n) else stdoutWrite("null")
            stdoutWriteln(",")
            self.print("\"endIndex\": ")
            if endNode |n| self.printNode(n) else stdoutWrite("null")
            stdoutWriteln()
          }
        }
      }
      TypedIndexingNode.Map(expr, idx) => {
        self.println("\"kind\": \"map\",")
        self.print("\"expr\": ")
        self.printNode(expr)
        stdoutWriteln(",")
        self.print("\"index\": ")
        self.printNode(idx)
        stdoutWriteln()
      }
      TypedIndexingNode.Tuple(expr, idx) => {
        self.println("\"kind\": \"tuple\",")
        self.println("\"index\": $idx,")
        self.print("\"expr\": ")
        self.printNode(expr)
        stdoutWriteln()
      }
    }

    self.indentDec()
    self.print("}")
  }

  func indent(self) = stdoutWrite("  ".repeat(self.indentLevel))

  func indentInc(self) { self.indentLevel += 1 }

  func indentDec(self) { self.indentLevel -= 1 }

  func print(self, str: String) {
    self.indent()
    stdoutWrite(str)
  }

  func println(self, str: String) {
    self.indent()
    stdoutWriteln(str)
  }

  func array<T>(self, arr: T[], fn: (T) => Unit) {
    if arr.isEmpty() {
      stdoutWrite("[]")
    } else {
      stdoutWriteln("[")
      self.indentInc()

      for item, idx in arr {
        self.indent()
        fn(item)

        val comma = if idx != arr.length - 1 "," else ""
        stdoutWriteln(comma)
      }

      self.indentDec()
      self.print("]")
    }
  }

  func opt<T>(self, v: T?, fn: (T) => Unit) {
    if v |v| {
      fn(v)
    } else {
      stdoutWrite("null")
    }
  }
}