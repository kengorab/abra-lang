func printlnBool(b: Bool) = stdoutWriteln(b.toString())

type Person {
  name: String
  age: Int
}

// Test raw construction of set
(() => {
  val set: Set<Int> = Set.new()
  set.insert(1)
  set.insert(2)
  set.insert(3)
  set.insert(3)

  /// Expect: 3
  stdoutWriteln(set.size.toString())
  /// Expect: #{1, 2, 3}
  stdoutWriteln(set.toString())
})()

// Test set literal construction
(() => {
  // Primitives
  val set1 = #{1, 2, 3, 3}

  /// Expect: 3
  stdoutWriteln(set1.size.toString())
  /// Expect: #{1, 2, 3}
  stdoutWriteln(set1.toString())

  // Objects
  val set2 = #{
    Person(name: "Foo", age: 100),
    Person(name: "Boo", age: 101),
    Person(name: "Goo", age: 102),
    Person(name: "Boo", age: 101),
  }
  /// Expect: 3
  stdoutWriteln(set2.size.toString())
  /// Expect: #{Person(name: "Goo", age: 102), Person(name: "Foo", age: 100), Person(name: "Boo", age: 101)}
  stdoutWriteln(set2.toString())
})()

// == operator (also Set#eq)
(() => {
  val set = #{1, 2, 3}

  /// Expect: true
  printlnBool(set == #{1, 2, 3})
  /// Expect: true
  printlnBool(#{[1, 2], [3, 4]} == #{[3, 4], [1, 2]})
  /// Expect: false
  printlnBool(set == #{1, 2, 3, 4})
})()

// Set#isEmpty
(() => {
  val set: Set<Int> = #{}
  /// Expect: true
  stdoutWriteln(set.isEmpty().toString())

  set.insert(1)
  /// Expect: false
  stdoutWriteln(set.isEmpty().toString())
})()

// Set#iterator
(() => {
  val s = #{"a", "b", "a", "c"}
  val iter = s.iterator()
  /// Expect: Option.Some(value: "c")
  stdoutWriteln(iter.next().toString())
  /// Expect: Option.Some(value: "b")
  stdoutWriteln(iter.next().toString())
  /// Expect: Option.Some(value: "a")
  stdoutWriteln(iter.next().toString())
  /// Expect: Option.None
  stdoutWriteln(iter.next().toString())
  /// Expect: Option.None
  stdoutWriteln(iter.next().toString())
})()

// For-loops
/// Expect: c 0
/// Expect: b 1
/// Expect: a 2
for ch, idx in #{"a", "b", "c"} {
  stdoutWriteln("$ch $idx")
}

// Set#contains
(() => {
  val set: Set<Int> = #{}
  /// Expect: false
  stdoutWriteln(set.contains(1).toString())

  set.insert(1)
  /// Expect: true
  stdoutWriteln(set.contains(1).toString())
  /// Expect: false
  stdoutWriteln(set.contains(12).toString())
})()

// Set#forEach
(() => {
  val set = #{1, 17, 0, 16}
  /// Expect: 0
  /// Expect: 16
  /// Expect: 1
  /// Expect: 17
  set.forEach(i => stdoutWriteln(i.toString()))
})()

// Set#map
(() => {
  val set = #{1, 17, 0, 16}
  /// Expect: [0, 0, 1, 1]
  stdoutWriteln(set.map(i => i % 16).toString())
})()

// Set#filter
(() => {
  val set = #{1, 17, 0, 16}
  /// Expect: #{16, 17}
  stdoutWriteln(set.filter(i => i >= 16).toString())
})()

// Set#asArray
(() => {
  val set1 = #{0, 1, 1, 2, 3, 3, 4}
  /// Expect: [0, 1, 2, 3, 4]
  stdoutWriteln(set1.asArray().toString())
})()

// Set#union
(() => {
  val evens = #{0, 2, 4, 6}
  val odds = #{1, 3, 5, 7}
  /// Expect: #{0, 1, 2, 3, 4, 5, 6, 7}
  stdoutWriteln(evens.union(odds).toString())
  /// Expect: #{0, 1, 2, 3, 4, 5, 6, 7}
  stdoutWriteln(odds.union(evens).toString())
})()

// Set#difference
(() => {
  val set1 = #{1, 2, 3, 4}
  val set2 = #{0, 2, 3, 5}
  /// Expect: #{1, 4}
  stdoutWriteln(set1.difference(set2).toString())
  /// Expect: #{0, 5}
  stdoutWriteln(set2.difference(set1).toString())
})()

// Set#intersection
(() => {
  val set1 = #{1, 2, 3, 4}
  val set2 = #{0, 2, 3, 5}
  /// Expect: #{2, 3}
  stdoutWriteln(set1.intersection(set2).toString())
  /// Expect: #{2, 3}
  stdoutWriteln(set2.intersection(set1).toString())
})()
