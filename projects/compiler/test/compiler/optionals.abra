func printlnBool(b: Bool) = stdoutWriteln(b.toString())

func f1(): Bool?[] = [None, Some(true), Some(false)]
/// Expect: [Option.None, Option.Some(value: true), Option.Some(value: false)]
stdoutWriteln(f1().toString())

func f2(): String?[] = if true { [Some("one"), None, Some("two")] } else []
/// Expect: [Option.Some(value: "one"), Option.None, Option.Some(value: "two")]
stdoutWriteln(f2().toString())

val arr1: Int?[] = [Some(1), None, Some(2), Some(3)]
/// Expect: [Option.Some(value: 1), Option.None, Option.Some(value: 2), Option.Some(value: 3)]
stdoutWriteln(arr1.toString())

val arr2: Int[]? = Some([1, 2, 3])
/// Expect: Option.Some(value: [1, 2, 3])
stdoutWriteln(arr2.toString())
val arr3: Int[]? = None
/// Expect: Option.None
stdoutWriteln(arr3.toString())

type Foo {
  f: String
}

// Workaround because optionals technically don't have _any_ directly-callable methods
func hash<T>(t: T): Int = t.hash()
// Hashes
(() => {
  val i: Int? = None
  /// Expect: 1
  stdoutWriteln(hash(i).toString())

  val x = Some(17)
  /// Expect: false
  printlnBool(hash(x) == (17).hash())
})()

// == operator
(() => {
  // Test with primitive
  val i1: Int? = None
  val i2 = Some(17)
  val i3 = Some(17)
  val i4 = Some(16)

  /// Expect: false
  printlnBool(i1 == i2)
  /// Expect: false
  printlnBool(i2 == i4)
  /// Expect: true
  printlnBool(i2 == i3)

  // Test with object
  val s1: String? = None
  val s2 = Some("str")
  val s3 = Some("str")
  val s4 = Some("hmm")

  /// Expect: false
  printlnBool(s1 == s2)
  /// Expect: false
  printlnBool(s2 == s4)
  /// Expect: true
  printlnBool(s2 == s3)
})()

// Coalescing (?:)
(() => {
  val i1: Int? = None
  /// Expect: 17
  stdoutWriteln((i1 ?: 17).toString())

  val i2 = Some(value: 9)
  /// Expect: 9
  stdoutWriteln((i2 ?: 17).toString())
})()

val xyz = "abc"
type TestOptChainingMethodInvocation {
  func unitMethod(self) = stdoutWriteln("TestOptChainingMethodInvocation#unitMethod")
  func closure(self): Int = xyz.length
}

// Optional-chaining accessor
(() => {
  val arr = ["a", "b", "c", "d"]

//   // Getting field of known-non-None value
//   /// Expect: 4
//   stdoutWriteln(arr?.length)
//   // Calling method of known-non-None value
//   /// Expect: [a, b, c, d]
//   stdoutWriteln(arr?.toString())

  // Getting field of Option value
  /// Expect: Option.Some(value: 1)
  stdoutWriteln(arr[0]?.length.toString())
  /// Expect: Option.None
  stdoutWriteln(arr[6]?.length.toString())

  // Calling method of Option value
  /// Expect: Option.Some(value: false)
  stdoutWriteln(arr[0]?.isEmpty().toString())
  /// Expect: Option.None
  stdoutWriteln(arr[6]?.isEmpty().toString())

  // nested opt-safe accessors
  val foo = Foo(f: "abc")
  val fooArr = [foo]

  /// Expect: Option.Some(value: 3)
  stdoutWriteln(fooArr[0]?.f?.length.toString())
  /// Expect: Option.Some(value: 3)
  stdoutWriteln(fooArr[0]?.f?.length?.abs().toString())

  // Calling unit method of Option value
  val t1: TestOptChainingMethodInvocation? = None
  t1?.unitMethod()
  val t2 = Some(TestOptChainingMethodInvocation())
  /// Expect: TestOptChainingMethodInvocation#unitMethod
  t2?.unitMethod()

  // Calling closure method of Option value
  val t3: TestOptChainingMethodInvocation? = None
  /// Expect: Option.None
  stdoutWriteln(t3?.closure().toString())
  val t4 = Some(TestOptChainingMethodInvocation())
  /// Expect: Option.Some(value: 3)
  stdoutWriteln(t4?.closure().toString())
})()

// Negate operator
val someInt = Some(123)
val noneInt: Int? = None

/// Expect: true
printlnBool(!noneInt)
/// Expect: false
printlnBool(!someInt)

/// Expect: false
printlnBool(!!noneInt)
/// Expect: true
printlnBool(!!someInt)

// Optional enum instances

enum Color {
  Red
  Green
  Blue
}
(() => {
  val colors = [Color.Red, Color.Green, Color.Blue]
  /// Expect: Option.None
  stdoutWriteln(colors[3]?.toString().toString())
  /// Expect: Option.Some(value: "Color.Blue")
  stdoutWriteln(colors[2]?.toString().toString())
})()
