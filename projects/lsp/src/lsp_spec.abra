import log from "./log"
import JsonValue, JsonError, JsonObject from "json"

pub enum RequestMessage {
  Initialize(id: Int, processId: Int?, rootPath: String?)
  Hover(id: Int, textDocument: TextDocumentIdentifier, position: Position)
  Definition(id: Int, textDocument: TextDocumentIdentifier, position: Position)

  func fromJson(json: JsonValue): Result<RequestMessage?, JsonError> {
    val obj = try json.asObject()
    val id = match try obj.getNumberRequired("id") {
      Either.Left(int) => int
      Either.Right(float) => float.asInt()
    }
    val method = try obj.getStringRequired("method")
    val params = obj.getObject("params")

    match method {
      "initialize" => {
        val params = try obj.getObjectRequired("params")
        val processId = match params.getNumber("processId") {
          None => None
          Either.Left(int) => Some(int)
          Either.Right(float) => Some(float.asInt())
        }
        val rootPath = params.getString("rootPath")

        Ok(Some(RequestMessage.Initialize(id: id, processId: processId, rootPath: rootPath)))
      }
      "textDocument/hover" => {
        val params = try obj.getObjectRequired("params")
        val textDocumentObj = try params.getValueRequired("textDocument")
        val textDocument = try TextDocumentIdentifier.fromJson(textDocumentObj)

        val positionObj = try params.getValueRequired("position")
        val position = try Position.fromJson(positionObj)

        Ok(Some(RequestMessage.Hover(id: id, textDocument: textDocument, position: position)))
      }
      "textDocument/definition" => {
        val params = try obj.getObjectRequired("params")
        val textDocumentObj = try params.getValueRequired("textDocument")
        val textDocument = try TextDocumentIdentifier.fromJson(textDocumentObj)

        val positionObj = try params.getValueRequired("position")
        val position = try Position.fromJson(positionObj)

        Ok(Some(RequestMessage.Definition(id: id, textDocument: textDocument, position: position)))
      }
      else => {
        log.writeln("Error: Unimplemented RequestMessage method '$method'")

        Ok(None)
      }
    }
  }
}

pub enum NotificationMessage {
  // Incoming notifications
  Initialized
  TextDocumentDidOpen(textDocument: TextDocumentItem)
  TextDocumentDidChange(textDocument: VersionedTextDocumentIdentifier, contentChanges: TextDocumentContentChangeEvent[])
  TextDocumentDidSave(textDocument: TextDocumentIdentifier)

  // Outgoing notifications
  TextDocumentPublishDiagnostics(uri: String, diagnostics: Diagnostic[])

  func fromJson(json: JsonValue): Result<NotificationMessage?, JsonError> {
    val obj = try json.asObject()
    val method = try obj.getStringRequired("method")

    match method {
      "initialized" => Ok(Some(NotificationMessage.Initialized))
      "textDocument/didOpen" => {
        val params = try obj.getObjectRequired("params")
        val textDocumentObj = try params.getValueRequired("textDocument")
        val textDocument = try TextDocumentItem.fromJson(textDocumentObj)

        Ok(Some(NotificationMessage.TextDocumentDidOpen(textDocument: textDocument)))
      }
      // "textDocument/didChange" => {
      //   val params = try obj.getObjectRequired("params")
      //   val textDocumentObj = try params.getValueRequired("textDocument")
      //   val textDocument = try VersionedTextDocumentIdentifier.fromJson(textDocumentObj)

      //   val contentChanges: TextDocumentContentChangeEvent[] = []
      //   val contentChangesArr = try params.getArrayRequired("contentChanges")
      //   for item in contentChangesArr {
      //     contentChanges.push(try TextDocumentContentChangeEvent.fromJson(item))
      //   }

      //   Ok(Some(NotificationMessage.TextDocumentDidChange(textDocument: textDocument, contentChanges: contentChanges)))
      // }
      "textDocument/didSave" => {
        val params = try obj.getObjectRequired("params")
        val textDocumentObj = try params.getValueRequired("textDocument")
        val textDocument = try TextDocumentIdentifier.fromJson(textDocumentObj)

        Ok(Some(NotificationMessage.TextDocumentDidSave(textDocument: textDocument)))
      }
      else => {
        log.writeln("Unimplemented NotificationMessage method '$method'")

        Ok(None)
      }
    }
  }

  func toJson(self): JsonValue {
    match self {
      NotificationMessage.Initialized => unreachable("should not be serializing 'initialized' notification")
      NotificationMessage.TextDocumentDidOpen => unreachable("should not be serializing 'textDocument/didOpen' notification")
      NotificationMessage.TextDocumentDidChange => unreachable("should not be serializing 'textDocument/didChange' notification")
      NotificationMessage.TextDocumentDidSave => unreachable("should not be serializing 'textDocument/didSave' notification")
      NotificationMessage.TextDocumentPublishDiagnostics(uri, diagnostics) => {
        JsonValue.Object(JsonObject.of({
          method: JsonValue.String("textDocument/publishDiagnostics"),
          params: JsonValue.Object(JsonObject.of({
            uri: JsonValue.String(uri),
            diagnostics: JsonValue.Array(diagnostics.map(d => d.toJson()))
          }))
        }))
      }
    }
  }
}

pub enum ResponseMessage {
  Success(id: Int, result: ResponseResult?)
  Error(id: Int, error: ResponseError)

  func toJson(self): JsonValue {
    val obj = JsonObject()

    match self {
      ResponseMessage.Success(id, result) => {
        obj.set("id", JsonValue.Number(Either.Left(id)))
        obj.set("result", result?.toJson() ?: JsonValue.Null)
      }
      ResponseMessage.Error(id, error) => {
        obj.set("id", JsonValue.Number(Either.Left(id)))
        obj.set("error", error.toJson())
      }
    }

    JsonValue.Object(obj)
  }
}

pub enum ResponseResult {
  Initialize(capabilities: ServerCapabilities, serverInfo: ServerInfo)
  Hover(contents: MarkupContent, range: Range?)
  // https://microsoft.github.io/language-server-protocol/specifications/lsp/3.17/specification/#textDocument_definition
  // Note: the actual response body for textDocument/definition requests is just a `Location` type, which is flattened here
  Definition(uri: String, range: Range)

  func toJson(self): JsonValue {
    val obj = match self {
      ResponseResult.Initialize(capabilities, serverInfo) => {
        JsonObject.of({
          "capabilities": capabilities.toJson(),
          "serverInfo": serverInfo.toJson(),
        })
      }
      ResponseResult.Hover(contents, range) => {
        val obj = JsonObject.of({
          contents: contents.toJson()
        })

        if range |range| {
          obj.set("range", range.toJson())
        }

        obj
      }
      ResponseResult.Definition(uri, range) => {
        JsonObject.of({
          "uri": JsonValue.String(uri),
          "range": range.toJson(),
        })
      }
    }

    JsonValue.Object(obj)
  }
}

pub type ResponseError {
  pub code: ResponseErrorCode
  pub message: String

  func toJson(self): JsonValue {
    val obj = JsonObject()

    obj.set("code", JsonValue.Number(Either.Left(self.code.intVal())))
    obj.set("message", JsonValue.String(self.message))

    JsonValue.Object(obj)
  }
}

pub enum ResponseErrorCode {
  ParseError
  InvalidRequest
  MethodNotFound
  InvalidParams
  InternalError
  ServerNotInitialized
  Unknown
  RequestFailed
  ServerCancelled
  ContentModified
  RequestCancelled

  func intVal(self): Int = match self {
    ResponseErrorCode.ParseError => -32700
    ResponseErrorCode.InvalidRequest => -32600
    ResponseErrorCode.MethodNotFound => -32601
    ResponseErrorCode.InvalidParams => -32602
    ResponseErrorCode.InternalError => -32603
    ResponseErrorCode.ServerNotInitialized => -32002
    ResponseErrorCode.Unknown => -32001
    ResponseErrorCode.RequestFailed => -32803
    ResponseErrorCode.ServerCancelled => -32802
    ResponseErrorCode.ContentModified => -32801
    ResponseErrorCode.RequestCancelled => -32800
  }
}

pub type ServerCapabilities {
  pub textDocumentSync: TextDocumentSyncOptions? = None
  pub diagnosticProvider: DiagnosticOptions? = None
  pub hoverProvider: Bool? = None
  pub definitionProvider: Bool? = None

  func toJson(self): JsonValue {
    val obj = JsonObject()

    if self.textDocumentSync |tds| {
      obj.set("textDocumentSync", tds.toJson())
    }

    if self.diagnosticProvider |dp| {
      obj.set("diagnosticProvider", dp.toJson())
    }

    if self.hoverProvider |hp| {
      obj.set("hoverProvider", JsonValue.Boolean(hp))
    }

    if self.definitionProvider |dp| {
      obj.set("definitionProvider", JsonValue.Boolean(dp))
    }

    JsonValue.Object(obj)
  }
}

pub type TextDocumentSyncOptions {
  pub openClose: Bool? = None
  pub change: TextDocumentSyncKind? = None
  pub save: SaveOptions? = None

  func toJson(self): JsonValue {
    val obj = JsonObject()

    if self.openClose |openClose| {
      obj.set("openClose", JsonValue.Boolean(openClose))
    }

    if self.change |change| {
      obj.set("change", JsonValue.Number(Either.Left(change.intVal())))
    }

    if self.save |save| {
      obj.set("save", save.toJson())
    }

    JsonValue.Object(obj)
  }
}

pub enum TextDocumentSyncKind {
  None_
  Full
  Incremental

  func intVal(self): Int = match self {
    TextDocumentSyncKind.None_ => 0
    TextDocumentSyncKind.Full => 1
    TextDocumentSyncKind.Incremental => 2
  }
}

pub type SaveOptions {
  pub includeText: Bool? = None

  func toJson(self): JsonValue {
    val obj = JsonObject()

    if self.includeText |includeText| {
      obj.set("includeText", JsonValue.Boolean(includeText))
    }

    JsonValue.Object(obj)
  }
}

pub type DiagnosticOptions {
  pub interFileDependencies: Bool
  pub workspaceDiagnostics: Bool

  func toJson(self): JsonValue = JsonValue.Object(JsonObject.of({
    interFileDependencies: JsonValue.Boolean(self.interFileDependencies),
    workspaceDiagnostics: JsonValue.Boolean(self.workspaceDiagnostics),
  }))
}

pub type ServerInfo {
  pub name: String
  pub version: String? = None

  func toJson(self): JsonValue {
    val obj = JsonObject()

    obj.set("name", JsonValue.String(self.name))
    if self.version |version| {
      obj.set("version", JsonValue.String(version))
    }

    JsonValue.Object(obj)
  }
}

pub type TextDocumentItem {
  pub uri: String
  pub languageId: String
  pub version: Int
  pub text: String

  func fromJson(json: JsonValue): Result<TextDocumentItem, JsonError> {
    val obj = try json.asObject()
    val uri = try obj.getStringRequired("uri")
    val languageId = try obj.getStringRequired("languageId")
    val version = match try obj.getNumberRequired("version") {
      Either.Left(int) => int
      Either.Right(float) => float.asInt()
    }
    val text = try obj.getStringRequired("text")

    Ok(TextDocumentItem(uri: uri, languageId: languageId, version: version, text: text))
  }
}

pub type TextDocumentIdentifier {
  pub uri: String

  func fromJson(json: JsonValue): Result<TextDocumentIdentifier, JsonError> {
    val obj = try json.asObject()
    val uri = try obj.getStringRequired("uri")

    Ok(TextDocumentIdentifier(uri: uri))
  }
}

pub type VersionedTextDocumentIdentifier {
  pub uri: String
  pub version: Int

  func fromJson(json: JsonValue): Result<VersionedTextDocumentIdentifier, JsonError> {
    val obj = try json.asObject()
    val uri = try obj.getStringRequired("uri")
    val version = match try obj.getNumberRequired("version") {
      Either.Left(int) => int
      Either.Right(float) => float.asInt()
    }

    Ok(VersionedTextDocumentIdentifier(uri: uri, version: version))
  }
}

pub type Position {
  // line and character are both zero-based
  pub line: Int
  pub character: Int

  func fromJson(json: JsonValue): Result<Position, JsonError> {
    val obj = try json.asObject()
    val line = match try obj.getNumberRequired("line") {
      Either.Left(int) => int
      Either.Right(float) => float.asInt()
    }
    val character = match try obj.getNumberRequired("character") {
      Either.Left(int) => int
      Either.Right(float) => float.asInt()
    }

    Ok(Position(line: line, character: character))
  }

  func toJson(self): JsonValue {
    JsonValue.Object(JsonObject.of({
      line: JsonValue.Number(Either.Left(self.line)),
      character: JsonValue.Number(Either.Left(self.character)),
    }))
  }
}

pub type Range {
  pub start: Position
  pub end: Position // exclusive

  func fromJson(json: JsonValue): Result<Range, JsonError> {
    val obj = try json.asObject()
    val startObj = try obj.getValueRequired("start")
    val start = try Position.fromJson(startObj)
    val endObj = try obj.getValueRequired("end")
    val end = try Position.fromJson(endObj)

    Ok(Range(start: start, end: end))
  }

  func toJson(self): JsonValue {
    JsonValue.Object(JsonObject.of({
      start: self.start.toJson(),
      end: self.end.toJson(),
    }))
  }
}

pub enum TextDocumentContentChangeEvent {
  Incremental(
    range: ((Int, Int), (Int, Int)), // (line: Int, character: Int), zero-based
    text: String
  )
  Full(text: String)

  func fromJson(json: JsonValue): Result<TextDocumentContentChangeEvent, JsonError> {
    val obj = try json.asObject()
    if obj.getObject("range") |range| {
      todo("parsing TextDocumentContentChangeEvent.Incremental")
    } else {
      val text = try obj.getStringRequired("text")

      Ok(TextDocumentContentChangeEvent.Full(text: text))
    }
  }
}

pub type Diagnostic {
  pub range: ((Int, Int), (Int, Int)) // (line: Int, character: Int), zero-based
  pub severity: DiagnosticSeverity? = None
  pub message: String

  func toJson(self): JsonValue {
    val obj = JsonObject()

    obj.set("range", JsonValue.Object(JsonObject.of({
      start: JsonValue.Object(JsonObject.of({
        line: JsonValue.Number(Either.Left(self.range[0][0])),
        character: JsonValue.Number(Either.Left(self.range[0][1])),
      })),
      end: JsonValue.Object(JsonObject.of({
        line: JsonValue.Number(Either.Left(self.range[1][0])),
        character: JsonValue.Number(Either.Left(self.range[1][1])),
      }))
    })))

    if self.severity |severity| {
      obj.set("severity", JsonValue.Number(Either.Left(severity.intVal())))
    }

    obj.set("message", JsonValue.String(self.message))

    JsonValue.Object(obj)
  }
}

pub enum DiagnosticSeverity {
  Error
  Warning
  Information
  Hint

  func intVal(self): Int = match self {
    DiagnosticSeverity.Error => 1
    DiagnosticSeverity.Warning => 2
    DiagnosticSeverity.Information => 3
    DiagnosticSeverity.Hint => 4
  }
}

pub type MarkupContent {
  pub kind: MarkupKind
  pub value: String

  func toJson(self): JsonValue {
    JsonValue.Object(JsonObject.of({
      kind: JsonValue.String(match self.kind {
        MarkupKind.PlainText => "plaintext"
        MarkupKind.Markdown => "markdown"
      }),
      value: JsonValue.String(self.value)
    }))
  }
}

pub enum MarkupKind {
  PlainText
  Markdown
}
