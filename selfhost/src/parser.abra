import Token, TokenKind, Position from "./lexer"

export type ParsedModule {
  imports: ImportNode[]
  nodes: AstNode[]
}

export enum ImportKind {
  List(imports: Label[])
  Alias(alias: Label)
}

type ImportNode {
  token: Token
  kind: ImportKind
  moduleName: Label
}

export type AstNode {
  token: Token
  kind: AstNodeKind
}

export enum LiteralAstNode {
  Int(value: Int)
  Float(value: Float)
  Bool(value: Bool)
  String(value: String)
}

export enum UnaryOp { Minus, Negate }

export type UnaryAstNode {
  op: UnaryOp
  expr: AstNode
}

enum BinaryOp {
  Add
  AddEq
  Sub
  SubEq
  Mul
  MulEq
  Div
  DivEq
  Mod
  ModEq
  Pow
  And
  AndEq
  Or
  OrEq
  Xor
  Coalesce
  CoalesceEq
  Eq
  Neq
  LT
  LTE
  Shl
  GT
  GTE
  Shr

  func isAssignment(self): Bool = match self {
    BinaryOp.AddEq => true
    BinaryOp.SubEq => true
    BinaryOp.MulEq => true
    BinaryOp.DivEq => true
    BinaryOp.ModEq => true
    BinaryOp.AndEq => true
    BinaryOp.OrEq => true
    BinaryOp.CoalesceEq => true
    _ => false
  }
}

type BinaryAstNode {
  left: AstNode
  op: BinaryOp
  right: AstNode
}

export enum IdentifierKind {
  Named(name: String)
  Self
  None_
  Discard
}

export type Label {
  name: String
  position: Position
}

type AccessorAstNode {
  root: AstNode
  path: (Token, Label)[]
}

type InvocationArgument {
  label: Label?
  value: AstNode
}

type InvocationAstNode {
  invokee: AstNode
  typeArguments: TypeIdentifier[]
  arguments: InvocationArgument[]
}

export enum IndexingMode {
  Single(expr: AstNode)
  Range(start: AstNode?, end: AstNode?)
}

export enum AssignmentMode {
  Variable(name: String)
  Indexing(expr: AstNode, indexExpr: AstNode)
  Accessor(node: AccessorAstNode)
}

export enum MatchCaseKind {
  None_
  Literal(value: LiteralAstNode)
  Type(path: Label[])
  Else
}

type MatchCase {
  kind: MatchCaseKind
  binding: Label?
  body: AstNode[]
}

export enum TypeIdentifier {
  Normal(name: Label, typeArguments: TypeIdentifier[]?)
  Array(inner: TypeIdentifier)
  Option(inner: TypeIdentifier)
  Tuple(types: TypeIdentifier[])
  Function(args: TypeIdentifier[], ret: TypeIdentifier)
}

export enum BindingPattern {
  Variable(label: Label)
}

export type BindingDeclarationNode {
  decorators: DecoratorNode[]
  exportToken: Token?
  bindingPattern: BindingPattern
  typeAnnotation: TypeIdentifier?
  expr: AstNode?
}

type FunctionParam {
  label: Label
  isVariadic: Bool = false
  typeAnnotation: TypeIdentifier? = None
  defaultValue: AstNode? = None
}

type LambdaNode {
  params: FunctionParam[]
  body: AstNode[]
}

export type FunctionDeclarationNode {
  decorators: DecoratorNode[]
  exportToken: Token?
  name: Label
  typeParams: Label[]
  params: FunctionParam[]
  returnTypeAnnotation: TypeIdentifier?
  body: AstNode[]
}

type TypeField {
  name: Label
  typeAnnotation: TypeIdentifier
  initializer: AstNode?
}

type TypeDeclarationNode {
  decorators: DecoratorNode[]
  exportToken: Token?
  name: Label
  typeParams: Label[]
  fields: TypeField[]
  methods: FunctionDeclarationNode[]
  types: TypeDeclarationNode[]
  enums: EnumDeclarationNode[]
}

export enum EnumVariant {
  Constant(label: Label)
  Container(label: Label, fields: TypeField[])
}

type EnumDeclarationNode {
  decorators: DecoratorNode[]
  exportToken: Token?
  name: Label
  typeParams: Label[]
  variants: EnumVariant[]
  methods: FunctionDeclarationNode[]
  types: TypeDeclarationNode[]
  enums: EnumDeclarationNode[]
}

export type DecoratorNode {
  name: Label
  arguments: InvocationArgument[]
}

export enum AstNodeKind {
  Literal(value: LiteralAstNode)
  Unary(value: UnaryAstNode)
  Binary(value: BinaryAstNode)
  Grouped(inner: AstNode)
  Identifier(kind: IdentifierKind)
  Accessor(value: AccessorAstNode)
  Invocation(value: InvocationAstNode)
  Array(items: AstNode[])
  Set(items: AstNode[])
  Map(items: (AstNode, AstNode)[])
  Tuple(items: AstNode[])
  Indexing(expr: AstNode, index: IndexingMode)
  Lambda(value: LambdaNode)
  Assignment(expr: AstNode, mode: AssignmentMode)
  If(condition: AstNode, conditionBinding: BindingPattern?, ifBlock: AstNode[], elseBlock: AstNode[]?)
  Match(expr: AstNode, cases: MatchCase[])
  While(condition: AstNode, conditionBinding: BindingPattern?, block: AstNode[])
  For(itemPattern: BindingPattern, indexPattern: BindingPattern?, iterator: AstNode, block: AstNode[])
  BindingDeclaration(value: BindingDeclarationNode)
  FunctionDeclaration(value: FunctionDeclarationNode)
  TypeDeclaration(value: TypeDeclarationNode)
  EnumDeclaration(value: EnumDeclarationNode)
  Break
  Continue
  Return(expr: AstNode?)

  func isAssignmentExpression(self): Bool {
    match self {
      AstNodeKind.Grouped(inner) => inner.kind.isAssignmentExpression()
      AstNodeKind.Assignment => true
      _ => false
    }
  }
}

enum ParseErrorKind {
  UnexpectedToken(token: Token)
  ExpectedToken(expected: TokenKind[], received: TokenKind)
  UnexpectedEof
  NotYetImplemented
  Unreachable
}

export type ParseError {
  position: Position
  kind: ParseErrorKind

  func getMessage(self, filePath: String, contents: String): String {
    val lines = ["Error at $filePath:${self.position.line}:${self.position.col}"]

    match self.kind {
      ParseErrorKind.UnexpectedToken(token) => {
        lines.push("Unexpected token '${token.kind.repr()}':")
        lines.push(self._getCursorLine(self.position, contents))
      }
      ParseErrorKind.ExpectedToken(expected, received) => {

        if expected.length == 1 {
          lines.push("Unexpected token '${received.repr()}', expected '${expected[0]?.repr()}':")
        } else {
          val reprs = expected.map(kind => "'${kind.repr()}'").join(", ")
          lines.push("Unexpected token '${received.repr()}', expected one of $reprs:")
        }
        lines.push(self._getCursorLine(self.position, contents))
      }
      ParseErrorKind.UnexpectedEof => {
        lines.push("Unexpected end of file:")
        lines.push(self._getCursorLine(self.position, contents))
      }
      ParseErrorKind.NotYetImplemented => {
        lines.push("Not yet implemented:")
        lines.push(self._getCursorLine(self.position, contents))
      }
      ParseErrorKind.Unreachable => {
        lines.push("Critical error: reached unreachable location in code!")
      }
    }

    lines.join("\n")
  }

  // TODO: This is duplicated from Lexer, pull this out
  func _getCursorLine(self, position: Position, contents: String, cursorLength = 1): String {
    if contents.lines()[position.line - 1] |line| {
      val len = position.col - 1 + cursorLength
      val cursor = Array.fill(len, " ")
      for i in range(len - cursorLength, len) {
        cursor[i] = "^"
      }
      "  |  $line\n     ${cursor.join()}"
    } else {
      "unreachable"
    }
  }
}

export type Parser {
  _tokens: Token[]
  _cursor: Int = 0
  _seenDecorators: DecoratorNode[] = []
  _exportToken: Token? = None

  func parse(tokens: Token[]): Result<ParsedModule, ParseError> {
    val parser = Parser(_tokens: tokens)

    val imports: ImportNode[] = []
    val nodes: AstNode[] = []

    var importsAllowed = true
    while parser._peek() |token| {
      if token.kind == TokenKind.Import {
        if !importsAllowed {
          return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.UnexpectedToken(token)))
        }

        val importNode = match parser._parseImport() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        imports.push(importNode)
        continue
      }
      importsAllowed = false

      match parser._parseStatement() {
        Result.Ok(node) => nodes.push(node)
        Result.Err(e) => return Result.Err(e)
      }
    }

    Result.Ok(ParsedModule(imports: imports, nodes: nodes))
  }

  func _peek(self, ahead = 0): Token? = self._tokens[self._cursor + ahead]

  func _expectPeek(self, ahead = 0): Result<Token, ParseError> {
    if self._peek(ahead) |token| {
      Result.Ok(token)
    } else {
      val position = if self._tokens[self._cursor - 1] |lastToken| {
        lastToken.position
      } else {
        Position(line: 1, col: 0)
      }
      // TODO: This isn't exactly correct (it should point just past the end of the token, and this would only work if
      // the token was of length 1), but it's close enough for now
      position.col += 1
      Result.Err(ParseError(position: position, kind: ParseErrorKind.UnexpectedEof))
    }
  }

  func _advance(self) {
    self._cursor += 1
  }

  func _expectNext(self): Result<Token, ParseError> {
    val tokenRes = self._expectPeek()
    self._advance()
    tokenRes
  }

  func _expectNextLabel(self): Result<Label, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    match token.kind {
      TokenKind.Ident(name) => Result.Ok(Label(name: name, position: token.position))
      _ => Result.Err(ParseError(position: token.position, kind: ParseErrorKind.ExpectedToken([TokenKind.Ident("")], token.kind)))
    }
  }

  func _expectNextTokenKind(self, kind: TokenKind): Result<Token, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    val nextTokenMatches = match kind {
      TokenKind.Int => match token.kind { TokenKind.Int => true, _ => false }
      TokenKind.Float => match token.kind { TokenKind.Float => true, _ => false }
      TokenKind.Bool => match token.kind { TokenKind.Bool => true, _ => false }
      TokenKind.String => match token.kind { TokenKind.String => true, _ => false }
      TokenKind.StringInterpolation => match token.kind { TokenKind.StringInterpolation => true, _ => false }
      TokenKind.Ident => match token.kind { TokenKind.Ident => true, _ => false }
      TokenKind.LParen => match token.kind { TokenKind.LParen => true, _ => false }
      TokenKind.LBrack=> match token.kind { TokenKind.LBrack => true, _ => false }
      _ => token.kind == kind
    }

    if nextTokenMatches {
      Result.Ok(token)
    } else {
      Result.Err(ParseError(position: token.position, kind: ParseErrorKind.ExpectedToken([kind], token.kind)))
    }
  }

  func _parseImport(self): Result<ImportNode, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val importNode = match nextToken.kind {
      TokenKind.String(string) => {
        val moduleName = Label(name: string, position: nextToken.position)
        self._advance() // consume string token

        val nextToken = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        if nextToken.kind != TokenKind.As {
          return Result.Err(ParseError(position: nextToken.position, kind: ParseErrorKind.ExpectedToken([TokenKind.As], nextToken.kind)))
        }

        val aliasName = match self._expectNextLabel() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        val kind = ImportKind.Alias(aliasName)

        ImportNode(token: token, kind: kind, moduleName: moduleName)
      }
      TokenKind.Ident => {
        val importsRes = self._commaSeparated(end: TokenKind.From, consumeFinal: true, fn: () => {
          val label = match self._expectNextLabel() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
          if label.name == "_" {
            val tok = Token(position: label.position, kind: TokenKind.Ident(label.name))
            Result.Err(ParseError(position: label.position, kind: ParseErrorKind.UnexpectedToken(tok)))
          } else {
            Result.Ok(label)
          }
        })
        val imports = match importsRes { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        val kind = ImportKind.List(imports)

        val nextToken = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        val moduleName = match nextToken.kind {
          TokenKind.String(name) => Label(name: name, position: nextToken.position)
          _ => return Result.Err(ParseError(position: nextToken.position, kind: ParseErrorKind.ExpectedToken([TokenKind.String("")], nextToken.kind)))
        }

        ImportNode(token: token, kind: kind, moduleName: moduleName)
      }
      _ => return Result.Err(ParseError(position: nextToken.position, kind: ParseErrorKind.ExpectedToken([TokenKind.String(""), TokenKind.Ident("")], nextToken.kind)))
    }

    Result.Ok(importNode)
  }

  func _parseStatement(self): Result<AstNode, ParseError> {
    val token = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    match token.kind {
      TokenKind.At => {
        match self._parseDecorator() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

        val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        val expected = [TokenKind.Val, TokenKind.Var, TokenKind.Func, TokenKind.Type, TokenKind.Enum, TokenKind.At, TokenKind.Export]
        if !expected.contains(nextToken.kind) {
          return Result.Err(ParseError(position: nextToken.position, kind: ParseErrorKind.ExpectedToken(expected, nextToken.kind)))
        }

        self._parseStatement()
      }
      TokenKind.Export => {
        self._exportToken = token
        self._advance() // consume 'export' token

        val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        val expected = [TokenKind.Val, TokenKind.Var, TokenKind.Func, TokenKind.Type, TokenKind.Enum]
        if !expected.contains(nextToken.kind) {
          return Result.Err(ParseError(position: nextToken.position, kind: ParseErrorKind.ExpectedToken(expected, nextToken.kind)))
        }

        self._parseStatement()
      }
      TokenKind.Val => self._parseBindingDeclaration(mutable: false)
      TokenKind.Var => self._parseBindingDeclaration(mutable: true)
      TokenKind.Func => self._parseFunctionDeclaration()
      TokenKind.Type => self._parseTypeDeclaration()
      TokenKind.Enum => self._parseEnumDeclaration()
      TokenKind.While => self._parseWhileLoop()
      TokenKind.For => self._parseForLoop()
      TokenKind.Break => self._parseBreak()
      TokenKind.Continue => self._parseContinue()
      TokenKind.Return => self._parseReturn()
      _ => self._parseExpressionStatement()
    }
  }

  func _parseDecorator(self): Result<Int, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    val decoratorName = match self._expectNextLabel() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val arguments = match nextToken.kind {
      TokenKind.LParen => {
        self._advance() // consume '(' token
        val arguments = match self._parseInvocationArguments() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        arguments
      }
      _ => []
    }

    for arg in arguments {
      match arg.value.kind {
        AstNodeKind.Literal => {}
        _ => {
          val expected = [TokenKind.Int(0), TokenKind.Float(0.0), TokenKind.Bool(true), TokenKind.String("")]
          return Result.Err(ParseError(position: arg.value.token.position, kind: ParseErrorKind.ExpectedToken(expected, arg.value.token.kind)))
        }
      }
    }

    val dec = DecoratorNode(name: decoratorName, arguments: arguments)
    self._seenDecorators.push(dec)

    Result.Ok(0) // <- unneeded, but I can't construct a Unit type, so...
  }

  func _parseBindingPattern(self): Result<BindingPattern, ParseError> {
    val token = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val pat = match token.kind {
      TokenKind.Ident => {
        val label = match self._expectNextLabel() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        BindingPattern.Variable(label)
      }
      _ => return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.ExpectedToken([TokenKind.Ident("")], token.kind)))
    }

    Result.Ok(pat)
  }

  func _parseBindingDeclaration(self, mutable: Bool): Result<AstNode, ParseError> {
    val decorators = self._seenDecorators
    self._seenDecorators = []

    val exportToken = self._exportToken
    self._exportToken = None

    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val pat = match self._parseBindingPattern() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    if self._cursor >= self._tokens.length {
      val node = BindingDeclarationNode(
        decorators: decorators,
        exportToken: exportToken,
        bindingPattern: pat,
        typeAnnotation: None,
        expr: None,
      )
      return Result.Ok(AstNode(token: token, kind: AstNodeKind.BindingDeclaration(node)))
    }

    var colonToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val typeAnnotation = if colonToken.kind == TokenKind.Colon {
      self._advance() // consume ':' token
      val a = match self._parseTypeIdentifier() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      a
    } else {
      None
    }

    if self._cursor >= self._tokens.length {
      val node = BindingDeclarationNode(
        decorators: decorators,
        exportToken: exportToken,
        bindingPattern: pat,
        typeAnnotation: typeAnnotation,
        expr: None,
      )
      return Result.Ok(AstNode(token: token, kind: AstNodeKind.BindingDeclaration(node)))
    }

    val eqToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val expr = if eqToken.kind == TokenKind.Eq {
      self._advance() // consume '=' token
      val e = match self._parseExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      e
    } else {
      None
    }

    val node = BindingDeclarationNode(
      decorators: decorators,
      exportToken: exportToken,
      bindingPattern: pat,
      typeAnnotation: typeAnnotation,
      expr: expr,
    )
    Result.Ok(AstNode(token: token, kind: AstNodeKind.BindingDeclaration(node)))
  }

  func _parseFunctionDeclaration(self): Result<AstNode, ParseError> {
    val decorators = self._seenDecorators
    self._seenDecorators = []

    val exportToken = self._exportToken
    self._exportToken = None

    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val label = match self._expectNextLabel() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    var nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val typeParams = match nextToken.kind {
      TokenKind.LT => {
        self._advance() // consume '<' token
        val labels = match self._parseTypeParameters() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        labels
      }
      TokenKind.LParen => []
      _ => return Result.Err(ParseError(position: nextToken.position, kind: ParseErrorKind.ExpectedToken([TokenKind.LParen(true)], nextToken.kind)))
    }

    // The `true` in LParen is disregarded within _expectNextTokenKind
    match self._expectNextTokenKind(TokenKind.LParen(true)) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    val params = match self._parseFunctionParameters(allowSelf: true) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val returnTypeAnnotation = if nextToken.kind == TokenKind.Colon {
      self._advance() // consume ':' token

      val ann = match self._parseTypeIdentifier() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      ann
    } else {
      None
    }

    val isStub = decorators.find(dec => {
      val decName = dec.name.name
      decName == "Stub" || decName == "Intrinsic" || decName == "CBinding"
    })

    val body: AstNode[] = if isStub {
      []
    } else {
      val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      if nextToken.kind == TokenKind.Eq {
        self._advance() // consume '=' token
        val expr = match self._parseExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        [expr]
      } else {
        match self._parseBlockOrSingleExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      }
    }

    val node = FunctionDeclarationNode(
      decorators: decorators,
      exportToken: exportToken,
      name: label,
      typeParams: typeParams,
      params: params,
      returnTypeAnnotation: returnTypeAnnotation,
      body: body,
    )
    Result.Ok(AstNode(token: token, kind: AstNodeKind.FunctionDeclaration(node)))
  }

  func _parseTypeDeclaration(self): Result<AstNode, ParseError> {
    val decorators = self._seenDecorators
    self._seenDecorators = []

    val exportToken = self._exportToken
    self._exportToken = None

    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val typeName = match self._expectNextLabel() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    var nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val typeParams = match nextToken.kind {
      TokenKind.LT => {
        self._advance() // consume '<' token
        val labels = match self._parseTypeParameters() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        labels
      }
      TokenKind.LBrace => []
      _ => return Result.Err(ParseError(position: nextToken.position, kind: ParseErrorKind.ExpectedToken([TokenKind.LParen(true)], nextToken.kind)))
    }

    match self._expectNextTokenKind(TokenKind.LBrace) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    val fields: TypeField[] = []
    while self._peek() |nextToken| {
      match nextToken.kind {
        TokenKind.Ident => {
          val field = match self._parseField() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

          val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
          if nextToken.kind == TokenKind.Comma {
            self._advance() // consume ',' token
          }

          fields.push(field)
        }
        _ => break
      }
    }

    val tuple = match self._parseBodyForTypeOrEnum() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val methods = tuple[0]
    val types = tuple[1]
    val enums = tuple[2]

    match self._expectNextTokenKind(TokenKind.RBrace) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    val node = TypeDeclarationNode(
      decorators: decorators,
      exportToken: exportToken,
      name: typeName,
      typeParams: typeParams,
      fields: fields,
      methods: methods,
      types: types,
      enums: enums,
    )
    Result.Ok(AstNode(token: token, kind: AstNodeKind.TypeDeclaration(node)))
  }

  func _parseEnumDeclaration(self): Result<AstNode, ParseError> {
    val decorators = self._seenDecorators
    self._seenDecorators = []

    val exportToken = self._exportToken
    self._exportToken = None

    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val enumName = match self._expectNextLabel() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    var nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val typeParams = match nextToken.kind {
      TokenKind.LT => {
        self._advance() // consume '<' token
        val labels = match self._parseTypeParameters() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        labels
      }
      TokenKind.LBrace => []
      _ => return Result.Err(ParseError(position: nextToken.position, kind: ParseErrorKind.ExpectedToken([TokenKind.LParen(true)], nextToken.kind)))
    }

    match self._expectNextTokenKind(TokenKind.LBrace) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    val variants: EnumVariant[] = []
    while self._peek() |nextToken| {
      match nextToken.kind {
        TokenKind.Ident => {
          val name = match self._expectNextLabel() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

          var nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
          val variant = match nextToken.kind {
            TokenKind.LParen => {
              self._advance() // consume '(' token

              val paramsRes = self._commaSeparated(end: TokenKind.RParen, consumeFinal: false, fn: () => self._parseField())
              val params = match paramsRes { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
              val rParen = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
              if params.isEmpty() {
                return Result.Err(ParseError(position: rParen.position, kind: ParseErrorKind.ExpectedToken([TokenKind.Ident("")], rParen.kind)))
              }
              EnumVariant.Container(name, params)
            }
            _ => EnumVariant.Constant(name)
          }

          nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
          if nextToken.kind == TokenKind.Comma {
            self._advance() // consume ',' token
          }
          variants.push(variant)
        }
        _ => break
      }
    }

    val tuple = match self._parseBodyForTypeOrEnum() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val methods = tuple[0]
    val types = tuple[1]
    val enums = tuple[2]

    match self._expectNextTokenKind(TokenKind.RBrace) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    val node = EnumDeclarationNode(
      decorators: decorators,
      exportToken: exportToken,
      name: enumName,
      typeParams: typeParams,
      variants: variants,
      methods: methods,
      types: types,
      enums: enums,
    )
    Result.Ok(AstNode(token: token, kind: AstNodeKind.EnumDeclaration(node)))
  }

  func _parseField(self): Result<TypeField, ParseError> {
    val name = match self._expectNextLabel() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    match self._expectNextTokenKind(TokenKind.Colon) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val typeAnnotation = match self._parseTypeIdentifier() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    var nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val initializer = if nextToken.kind == TokenKind.Eq {
      self._advance() // consume '=' token
      val expr = match self._parseExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      expr
    } else {
      None
    }

    Result.Ok(TypeField(name: name, typeAnnotation: typeAnnotation, initializer: initializer))
  }

  func _parseBodyForTypeOrEnum(self): Result<(FunctionDeclarationNode[], TypeDeclarationNode[], EnumDeclarationNode[]), ParseError> {
    val methods: FunctionDeclarationNode[] = []
    val types: TypeDeclarationNode[] = []
    val enums: EnumDeclarationNode[] = []
    while self._peek() |nextToken| {
      if nextToken.kind == TokenKind.RBrace break
      val nodeRes = match nextToken.kind {
        TokenKind.RBrace => break
        TokenKind.At => self._parseStatement()
        TokenKind.Func => self._parseStatement()
        TokenKind.Type => self._parseStatement()
        TokenKind.Enum => self._parseStatement()
        _ => return Result.Err(ParseError(position: nextToken.position, kind: ParseErrorKind.UnexpectedToken(nextToken)))
      }

      val node = match nodeRes { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      match node.kind {
        AstNodeKind.FunctionDeclaration(node) => methods.push(node)
        AstNodeKind.TypeDeclaration(node) => types.push(node)
        AstNodeKind.EnumDeclaration(node) => enums.push(node)
        _ => return unreachable()
      }
    }

    Result.Ok((methods, types, enums))
  }

  func _parseTypeIdentifier(self): Result<TypeIdentifier, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    var typeIdent =  match token.kind {
      TokenKind.Ident(name) => {
        val label = Label(name: name, position: token.position)

        // TODO: This is kind of gross, I should be able to do `if self._peek()?.kind == TokenKind.LT { ... }` here
        val typeArguments = if self._peek()?.kind |kind| {
          if kind == TokenKind.LT {
            self._advance() // consume '<' token
            val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
            if nextToken.kind == TokenKind.GT {
              return Result.Err(ParseError(position: nextToken.position, kind: ParseErrorKind.ExpectedToken([TokenKind.Ident("")], nextToken.kind)))
            }
            val args = self._commaSeparated(end: TokenKind.GT, consumeFinal: true, fn: () => self._parseTypeIdentifier())
            val a = match args { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
            a
          } else {
            None
          }
        } else {
          None
        }

        TypeIdentifier.Normal(name: label, typeArguments: typeArguments)
      }
      TokenKind.LParen => {
        val a = self._commaSeparated(end: TokenKind.RParen, consumeFinal: true, fn: () => self._parseTypeIdentifier())
        val args = match a { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

        val nextTokenIsArrow = if self._peek()?.kind |kind| kind == TokenKind.Arrow else false
        if nextTokenIsArrow {
          self._advance() // consume '=>' token
          val retType = match self._parseTypeIdentifier() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
          TypeIdentifier.Function(args, retType)
        } else if args[0] |first| {
          // TODO: There should be a more ergonomic way of writing this... maybe like
          // `if args.length == 1 && args[0] |first| { ... }` ?
          if args.length == 1 {
            first
          } else {
            TypeIdentifier.Tuple(args)
          }
        } else {
          val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
          return Result.Err(ParseError(position: nextToken.position, kind: ParseErrorKind.ExpectedToken([TokenKind.Arrow], nextToken.kind)))
        }
      }
      _ => return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.ExpectedToken([TokenKind.Ident("")], token.kind)))
    }

    while self._peek() |nextToken| {
      match nextToken.kind {
        TokenKind.LBrack(preceedingNewline) => if preceedingNewline {
          break
        } else {
          self._advance() // consume '[' token
          match self._expectNextTokenKind(TokenKind.RBrack) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

          typeIdent = TypeIdentifier.Array(inner: typeIdent)
        }
        TokenKind.Question => {
          self._advance() // consume '?' token
          typeIdent = TypeIdentifier.Option(inner: typeIdent)
        }
        _ => break
      }
    }

    Result.Ok(typeIdent)
  }

  func _parseFunctionParameters(self, allowSelf: Bool): Result<FunctionParam[], ParseError> {
    self._commaSeparated(end: TokenKind.RParen, consumeFinal: true, fn: paramIdx => {
      var token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      val isVariadic = if token.kind == TokenKind.Star {
        token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        true
      } else {
        false
      }

      val label = match token.kind {
        TokenKind.Ident(name) => Label(name: name, position: token.position)
        TokenKind.Self => if allowSelf && paramIdx == 0 {
          Label(name: "self", position: token.position)
        } else {
          return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.ExpectedToken([TokenKind.Ident("")], token.kind)))
        }
        _ => return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.ExpectedToken([TokenKind.Ident("")], token.kind)))
      }

      var nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      val typeAnnotation = if nextToken.kind == TokenKind.Colon {
        self._advance() // consume ':' token
        val ann = match self._parseTypeIdentifier() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        ann
      } else {
        None
      }

      nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      val defaultValue = if nextToken.kind == TokenKind.Eq {
        self._advance() // consume '=' token
        val e = match self._parseExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        e
      } else {
        None
      }

      val param = FunctionParam(label: label, isVariadic: isVariadic, typeAnnotation: typeAnnotation, defaultValue: defaultValue)
      Result.Ok(param)
    })
  }

  func _parseTypeParameters(self): Result<Label[], ParseError> {
    val labels = match self._commaSeparated(end: TokenKind.GT, consumeFinal: false, fn: () => self._expectNextLabel()) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    if labels.isEmpty() {
      val token = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      Result.Err(ParseError(position: token.position, kind: ParseErrorKind.ExpectedToken([TokenKind.Ident("")], token.kind)))
    } else {
      self._advance() // consume '>' token
      Result.Ok(labels)
    }
  }

  func _parseBlockOrSingleExpression(self, allowTerminators = false): Result<AstNode[], ParseError> {
    val token = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val nodes = match token.kind {
      TokenKind.LBrace => {
        self._advance() // consume '{' token
        val nodes: AstNode[] = []
        while true {
          val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
          if nextToken.kind == TokenKind.RBrace {
            self._advance() // consume '}' token
            break
          }

          val node = match self._parseStatement() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
          nodes.push(node)
        }
        nodes
      }
      TokenKind.Break => if allowTerminators {
        val node = match self._parseStatement() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        [node]
      } else {
        return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.UnexpectedToken(token)))
      }
      TokenKind.Continue => if allowTerminators {
        val node = match self._parseStatement() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        [node]
      } else {
        return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.UnexpectedToken(token)))
      }
      TokenKind.Return => if allowTerminators {
        val node = match self._parseStatement() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        [node]
      } else {
        return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.UnexpectedToken(token)))
      }
      _ => {
        val node = match self._parseExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        [node]
      }
    }

    Result.Ok(nodes)
  }

  func _parseExpressionStatement(self): Result<AstNode, ParseError> = self._parsePrecedence(prec: 0)

  func _parseExpression(self): Result<AstNode, ParseError> {
    val node = match self._parsePrecedence(prec: 0) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    if node.kind.isAssignmentExpression() {
      return Result.Err(ParseError(position: node.token.position, kind: ParseErrorKind.UnexpectedToken(node.token)))
    }
    Result.Ok(node)
  }

  func _parsePrecedence(self, prec: Int): Result<AstNode, ParseError> {
    val prefixToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    var leftNode = match self._prefixRule(prefixToken) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    while self._peek() |nextToken| {
      val nextPrec = Precedence.forToken(nextToken)
      if prec < nextPrec {
        leftNode = match self._infixRule(leftNode) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      } else {
        break
      }
    }

    Result.Ok(leftNode)
  }

  func _prefixRule(self, token: Token): Result<AstNode, ParseError> {
    match token.kind {
      TokenKind.Int => self._parseLiteral()
      TokenKind.Float => self._parseLiteral()
      TokenKind.Bool => self._parseLiteral()
      TokenKind.String => self._parseLiteral()
      TokenKind.LParen => self._parseGroupedOrTupleOrLambda()
      TokenKind.Minus => self._parseUnary()
      TokenKind.Bang => self._parseUnary()
      TokenKind.Ident(name) => self._parseIdentifier(kind: if name == "_" IdentifierKind.Discard else IdentifierKind.Named(name))
      TokenKind.None_ => self._parseIdentifier(kind: IdentifierKind.None_)
      TokenKind.Self => self._parseIdentifier(kind: IdentifierKind.Self)
      TokenKind.LBrack => self._parseArray()
      TokenKind.HashBrace => self._parseSet()
      TokenKind.LBrace => self._parseMap()
      TokenKind.If => self._parseIf()
      TokenKind.Match => self._parseMatch()
      _ => Result.Err(ParseError(position: token.position, kind: ParseErrorKind.UnexpectedToken(token)))
    }
  }

  func _infixRule(self, left: AstNode): Result<AstNode, ParseError> {
    val infixToken = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    match infixToken.kind {
      TokenKind.Dot => self._parseAccessor(infixToken, left)
      TokenKind.QuestionDot => self._parseAccessor(infixToken, left)
      TokenKind.LParen => self._parseInvocation(infixToken, left)
      TokenKind.LBrack => self._parseIndexing(infixToken, left)
      TokenKind.Arrow => self._parseLambda(infixToken, left)
      TokenKind.Eq => self._parseAssignment(infixToken, left)
      _ => self._parseBinary(infixToken, left)
    }
  }

  func _parseLiteral(self): Result<AstNode, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val literal = match token.kind {
      TokenKind.Int(value) => LiteralAstNode.Int(value)
      TokenKind.Float(value) => LiteralAstNode.Float(value)
      TokenKind.Bool(value) => LiteralAstNode.Bool(value)
      TokenKind.String(value) => LiteralAstNode.String(value)
      TokenKind.StringInterpolation => return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.NotYetImplemented))
      _ => return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.UnexpectedToken(token)))
    }

    Result.Ok(AstNode(token: token, kind: AstNodeKind.Literal(literal)))
  }

  func _parseUnary(self): Result<AstNode, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val op = match token.kind {
      TokenKind.Minus => UnaryOp.Minus
      TokenKind.Bang => UnaryOp.Negate
      _ => return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.NotYetImplemented))
    }

    val expr = match self._parsePrecedence(prec: Precedence.unary()) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    Result.Ok(AstNode(token: token, kind: AstNodeKind.Unary(UnaryAstNode(op: op, expr: expr))))
  }

  func _parseGroupedOrTupleOrLambda(self): Result<AstNode, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    // If the next token is ')' then we must be parsing a no-args lambda
    match nextToken.kind {
      TokenKind.RParen => {
        self._advance() // consume ')' token
        val arrowToken = match self._expectNextTokenKind(TokenKind.Arrow) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        val body = match self._parseBlockOrSingleExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

        return Result.Ok(AstNode(token: arrowToken, kind: AstNodeKind.Lambda(LambdaNode(params: [], body: body))))
      }
      TokenKind.Ident => {
        val nextToken = match self._expectPeek(ahead: 1) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        if nextToken.kind == TokenKind.Colon || nextToken.kind == TokenKind.Eq {
          val params = match self._parseFunctionParameters(allowSelf: false) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
          val arrowToken = match self._expectNextTokenKind(TokenKind.Arrow) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
          val body = match self._parseBlockOrSingleExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

          return Result.Ok(AstNode(token: arrowToken, kind: AstNodeKind.Lambda(LambdaNode(params: params, body: body))))
        }
      }
      TokenKind.Star => { // a variadic parameter could be first
        val params = match self._parseFunctionParameters(allowSelf: false) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        val arrowToken = match self._expectNextTokenKind(TokenKind.Arrow) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        val body = match self._parseBlockOrSingleExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

        return Result.Ok(AstNode(token: arrowToken, kind: AstNodeKind.Lambda(LambdaNode(params: params, body: body))))
      }
      _ => {}
    }

    val exprs = match self._commaSeparated(end: TokenKind.RParen, consumeFinal: true, fn: () => self._parseExpression()) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val nodeKind = if exprs.length == 1 {
      // TODO: Again, there needs to be a more ergonomic way of doing this. `if exprs.length == 1 && exprs[0] |inner| { ... }` maybe? or even just `exprs[0].!` to force-unwrap
      if exprs[0] |inner| {
        AstNodeKind.Grouped(inner: inner)
      } else {
        return unreachable()
      }
    } else {
      AstNodeKind.Tuple(items: exprs)
    }
    Result.Ok(AstNode(token: token, kind: nodeKind))
  }

  func _tryParseInvocationWithTypeArgs(self, invokee: AstNode): Result<AstNode?, ParseError> {
    if self._peek() |nextToken| {
      if nextToken.kind == TokenKind.LT {
        val savedCursor = self._cursor
        self._advance() // consume '<' token (cursor may be rewound later)

        match self._commaSeparated(end: TokenKind.GT, consumeFinal: true, fn: () => self._parseTypeIdentifier()) {
          Result.Ok(typeArgs) => {
            if typeArgs.isEmpty() {
              self._cursor = savedCursor
            } else {
              val nextToken = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
              if nextToken.kind != TokenKind.LParen(false) {
                return Result.Err(ParseError(position: nextToken.position, kind: ParseErrorKind.ExpectedToken([TokenKind.LParen(false)], nextToken.kind)))
              }

              val arguments = match self._parseInvocationArguments() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
              val invocationNode = InvocationAstNode(invokee: invokee, typeArguments: typeArgs, arguments: arguments)
              return Result.Ok(AstNode(token: nextToken, kind: AstNodeKind.Invocation(invocationNode)))
            }
          }
          Result.Err => {
            // Restore saved cursor; the failed attempt to parse the subsequent tokens as type identifiers indicates that it should be parsed in some
            // other way. Fallthrough and return a None value. This indicates to the caller that no invocation with type arguments was processed, and
            // that the tokens should instead be parsed as normal.
            self._cursor = savedCursor
          }
        }
      }
    }

    Result.Ok(None)
  }

  func _parseIdentifier(self, kind: IdentifierKind): Result<AstNode, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val identNode = AstNode(token: token, kind: AstNodeKind.Identifier(kind))

    val res = match self._tryParseInvocationWithTypeArgs(invokee: identNode) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    if res |invocationNode| return Result.Ok(invocationNode)

    Result.Ok(identNode)
  }

  func _parseArray(self): Result<AstNode, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val itemsRes = self._commaSeparated(end: TokenKind.RBrack, consumeFinal: true, fn: () => self._parseExpression())
    val items = match itemsRes { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    Result.Ok(AstNode(token: token, kind: AstNodeKind.Array(items)))
  }

  func _parseSet(self): Result<AstNode, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val itemsRes = self._commaSeparated(end: TokenKind.RBrace, consumeFinal: true, fn: () => self._parseExpression())
    val items = match itemsRes { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    Result.Ok(AstNode(token: token, kind: AstNodeKind.Set(items)))
  }

  func _parseMap(self): Result<AstNode, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val itemsRes = self._commaSeparated(end: TokenKind.RBrace, consumeFinal: true, fn: () => {
      val keyToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      val keyRes = match keyToken.kind {
        TokenKind.String => self._parseExpression()
        TokenKind.Ident => self._parseExpression()
        TokenKind.LParen => {
          self._advance() // consume '(' token
          val key = self._parseExpression()
          match self._expectNextTokenKind(TokenKind.RParen) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
          key
        }
        _ => {
          return Result.Err(ParseError(position: keyToken.position, kind: ParseErrorKind.ExpectedToken([TokenKind.String(""), TokenKind.Ident(""), TokenKind.LParen(false)], keyToken.kind)))
        }
      }
      val key = match keyRes { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

      match self._expectNextTokenKind(TokenKind.Colon) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

      val value = match self._parseExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

      Result.Ok((key, value))
    })
    val items = match itemsRes { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    Result.Ok(AstNode(token: token, kind: AstNodeKind.Map(items)))
  }

  func _parseIf(self): Result<AstNode, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val condition = match self._parseExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val conditionBinding = if nextToken.kind == TokenKind.Pipe {
      self._advance() // consume opening '|' token
      val pat = match self._parseBindingPattern() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      match self._expectNextTokenKind(TokenKind.Pipe) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      pat
    } else {
      None
    }

    val ifBlock = match self._parseBlockOrSingleExpression(allowTerminators: true) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val elseBlock = if self._peek() |nextToken| {
      if nextToken.kind == TokenKind.Else {
        self._advance() // consume 'else' token
        val elseBlock = match self._parseBlockOrSingleExpression(allowTerminators: true) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        elseBlock
      } else {
        None
      }
    } else {
      None
    }

    Result.Ok(AstNode(token: token, kind: AstNodeKind.If(condition, conditionBinding, ifBlock, elseBlock)))
  }

  func _parseMatch(self): Result<AstNode, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val expr = match self._parseExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    match self._expectNextTokenKind(TokenKind.LBrace) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    val cases: MatchCase[] = []
    while self._peek() |next| {
      val caseKind = match next.kind {
        TokenKind.RBrace => break
        TokenKind.None_ => {
          self._advance() // consume 'None' token
          MatchCaseKind.None_
        }
        TokenKind.Int(value) => {
          self._advance() // consume int token
          MatchCaseKind.Literal(LiteralAstNode.Int(value))
        }
        TokenKind.Float(value) => {
          self._advance() // consume float token
          MatchCaseKind.Literal(LiteralAstNode.Float(value))
        }
        TokenKind.Bool(value) => {
          self._advance() // consume bool token
          MatchCaseKind.Literal(LiteralAstNode.Bool(value))
        }
        TokenKind.String(value) => {
          self._advance() // consume string token
          MatchCaseKind.Literal(LiteralAstNode.String(value))
        }
        TokenKind.Ident(name) => {
          if name == "_" {
            self._advance() // consume '_' token
            MatchCaseKind.Else
          } else {
            val label = match self._expectNextLabel() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
            val path = [label]
            while self._peek() |nextToken| {
              if nextToken.kind != TokenKind.Dot break
              self._advance() // consume '.' token
              val label = match self._expectNextLabel() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
              path.push(label)
            }

            // TODO: Destructuring types in match cases
            val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
            match nextToken.kind {
              TokenKind.LParen => return todo(nextToken.position)
              _ => {}
            }

            MatchCaseKind.Type(path)
          }
        }
        TokenKind.Else => {
          self._advance() // consume 'else' token
          MatchCaseKind.Else
        }
        _ => {
          val expected = [TokenKind.RBrace, TokenKind.Int(0), TokenKind.Float(0.0), TokenKind.Bool(true), TokenKind.String(""), TokenKind.Ident(""), TokenKind.Else]
          return Result.Err(ParseError(position: next.position, kind: ParseErrorKind.ExpectedToken(expected, next.kind)))
        }
      }

      var nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      val binding: Label? = match nextToken.kind {
        TokenKind.Ident => {
          val label = match self._expectNextLabel() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
          label
        }
        _ => None
      }

      nextToken = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      if nextToken.kind != TokenKind.Arrow {
        val expected = if binding { [TokenKind.Arrow] } else [TokenKind.Ident(""), TokenKind.Arrow]
        return Result.Err(ParseError(position: nextToken.position, kind: ParseErrorKind.ExpectedToken(expected, nextToken.kind)))
      }

      val body = match self._parseBlockOrSingleExpression(allowTerminators: true) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      if nextToken.kind == TokenKind.Comma {
        self._advance() // consume ',' token
      }

      cases.push(MatchCase(kind: caseKind, binding: binding, body: body))
    }
    self._advance() // consume '}' token

    Result.Ok(AstNode(token: token, kind: AstNodeKind.Match(expr, cases)))
  }

  func _parseWhileLoop(self): Result<AstNode, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val condition = match self._parseExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val conditionBinding = if nextToken.kind == TokenKind.Pipe {
      self._advance() // consume opening '|' token
      val pat = match self._parseBindingPattern() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      match self._expectNextTokenKind(TokenKind.Pipe) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      pat
    } else {
      None
    }

    val block = match self._parseBlockOrSingleExpression(allowTerminators: true) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    Result.Ok(AstNode(token: token, kind: AstNodeKind.While(condition, conditionBinding, block)))
  }

  func _parseForLoop(self): Result<AstNode, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val itemPattern = match self._parseBindingPattern() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    var nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val indexPattern = if nextToken.kind == TokenKind.Comma {
      self._advance() // consume ',' token
      val pat = match self._parseBindingPattern() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      pat
    } else {
      None
    }

    match self._expectNextTokenKind(TokenKind.In) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    val iterator = match self._parseExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    val block = match self._parseBlockOrSingleExpression(allowTerminators: true) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    Result.Ok(AstNode(token: token, kind: AstNodeKind.For(itemPattern, indexPattern, iterator, block)))
  }

  func _parseBreak(self): Result<AstNode, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    Result.Ok(AstNode(token: token, kind: AstNodeKind.Break))
  }

  func _parseContinue(self): Result<AstNode, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    Result.Ok(AstNode(token: token, kind: AstNodeKind.Continue))
  }

  func _parseReturn(self): Result<AstNode, ParseError> {
    val token = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val hasNewline = match token.kind {
      TokenKind.Return(hasNewline) => hasNewline
      _ => false // unreachable
    }

    val expr: AstNode? = if hasNewline {
      None
    } else {
      val expr = match self._parseExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      expr
    }
    Result.Ok(AstNode(token: token, kind: AstNodeKind.Return(expr)))
  }

  func _parseBinary(self, token: Token, left: AstNode): Result<AstNode, ParseError> {
    val prec = Precedence.forToken(token)
    val op = match token.kind {
      TokenKind.Plus => BinaryOp.Add
      TokenKind.PlusEq => BinaryOp.AddEq
      TokenKind.Minus => BinaryOp.Sub
      TokenKind.MinusEq => BinaryOp.SubEq
      TokenKind.Star => BinaryOp.Mul
      TokenKind.StarEq => BinaryOp.MulEq
      TokenKind.Slash => BinaryOp.Div
      TokenKind.SlashEq => BinaryOp.DivEq
      TokenKind.Percent => BinaryOp.Mod
      TokenKind.PercentEq => BinaryOp.ModEq
      TokenKind.StarStar => BinaryOp.Pow
      TokenKind.And => BinaryOp.And
      TokenKind.AndEq => BinaryOp.AndEq
      TokenKind.Or => BinaryOp.Or
      TokenKind.OrEq => BinaryOp.OrEq
      TokenKind.Caret => BinaryOp.Xor
      TokenKind.Elvis => BinaryOp.Coalesce
      TokenKind.ElvisEq => BinaryOp.CoalesceEq
      TokenKind.EqEq => BinaryOp.Eq
      TokenKind.Neq => BinaryOp.Neq
      TokenKind.LT => {
        val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        if nextToken.kind == TokenKind.LT {
          self._advance() // consume '<' token
          BinaryOp.Shl
        } else {
          BinaryOp.LT
        }
      }
      TokenKind.LTE => BinaryOp.LTE
      TokenKind.GT => {
        val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        if nextToken.kind == TokenKind.GT {
          self._advance() // consume '>' token
          BinaryOp.Shr
        } else {
          BinaryOp.GT
        }
      }
      TokenKind.GTE => BinaryOp.GTE
      _ => return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.UnexpectedToken(token)))
    }

    if op.isAssignment() {
      val isValidLvalue = match left.kind {
        AstNodeKind.Identifier(identKind) => match identKind { IdentifierKind.Named => true, _ => false }
        AstNodeKind.Accessor => true
        AstNodeKind.Indexing(_, indexMode) => match indexMode { IndexingMode.Single => true, _ => false }
        _ => false
      }

      if !isValidLvalue {
        return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.UnexpectedToken(token)))
      }
    }

    val right = match self._parsePrecedence(prec: prec) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    Result.Ok(AstNode(token: token, kind: AstNodeKind.Binary(BinaryAstNode(left: left, op: op, right: right))))
  }

  func _parseAccessor(self, token: Token, left: AstNode): Result<AstNode, ParseError> {
    val label = match self._expectNextLabel() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    val accessorNode = match left.kind {
      AstNodeKind.Accessor(accessorNode) => accessorNode
      _ => AccessorAstNode(root: left, path: [])
    }
    accessorNode.path.push((token, label))

    val node = AstNode(token: token, kind: AstNodeKind.Accessor(accessorNode))

    val res = match self._tryParseInvocationWithTypeArgs(invokee: node) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    if res |invocationNode| return Result.Ok(invocationNode)

    Result.Ok(node)
  }

  func _commaSeparated<T>(self, end: TokenKind, consumeFinal: Bool, fn: (Int) => Result<T, ParseError>): Result<T[], ParseError> {
    val items: T[] = []

    var endExpected = false
    var idx = 0
    while true {
      val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      if nextToken.kind == end {
        if consumeFinal self._advance() // consume end token before exiting
        break
      } else if endExpected {
        val kind = ParseErrorKind.ExpectedToken(expected: [TokenKind.Comma, end], received: nextToken.kind)
        return Result.Err(ParseError(position: nextToken.position, kind: kind))
      }

      val item = match fn(idx) { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      items.push(item)

      val peekToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      if peekToken.kind == TokenKind.Comma {
        self._advance() // consume ',' token
      } else {
        endExpected = true
      }

      idx += 1
    }

    Result.Ok(items)
  }

  func _parseInvocationArguments(self): Result<InvocationArgument[], ParseError> {
    val argsRes = self._commaSeparated(end: TokenKind.RParen, consumeFinal: true, fn: () => {
      val expr = match self._parseExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      val arg = match expr.kind {
        AstNodeKind.Identifier(identifierKind) => {
          val peekToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
          val arg = if peekToken.kind == TokenKind.Colon {
            self._advance() // consume ':' token

            val label = match identifierKind {
              IdentifierKind.Named(name) => Label(name: name, position: expr.token.position)
              _ => {
                val kind = ParseErrorKind.ExpectedToken(expected: [TokenKind.Ident("")], received: expr.token.kind)
                return Result.Err(ParseError(position: expr.token.position, kind: kind))
              }
            }
            val value = match self._parseExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

            InvocationArgument(label: label, value: value)
          } else {
            InvocationArgument(label: None, value: expr)
          }
          arg
        }
        _ => InvocationArgument(label: None, value: expr)
      }

      Result.Ok(arg)
    })
    val arguments = match argsRes { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    Result.Ok(arguments)
  }

  func _parseInvocation(self, token: Token, left: AstNode): Result<AstNode, ParseError> {
    val arguments = match self._parseInvocationArguments() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    Result.Ok(AstNode(token: token, kind: AstNodeKind.Invocation(InvocationAstNode(invokee: left, typeArguments: [], arguments: arguments))))
  }

  func _parseIndexing(self, token: Token, left: AstNode): Result<AstNode, ParseError> {
    val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    val index = if nextToken.kind == TokenKind.Colon {
      self._advance() // consume ':' token
      val rangeEnd = match self._parseExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      IndexingMode.Range(start: None, end: rangeEnd)
    } else {
      val expr = match self._parseExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
      if nextToken.kind == TokenKind.RBrack {
        IndexingMode.Single(expr)
      } else if nextToken.kind == TokenKind.Colon {
        self._advance() // consume ':' token
        val nextToken = match self._expectPeek() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
        if nextToken.kind == TokenKind.RBrack {
          IndexingMode.Range(start: expr, end: None)
        } else {
          val rangeEnd = match self._parseExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
          IndexingMode.Range(start: expr, end: rangeEnd)
        }
      } else {
        return Result.Err(ParseError(position: nextToken.position, kind: ParseErrorKind.ExpectedToken([TokenKind.Colon, TokenKind.RBrack], nextToken.kind)))
      }
    }

    val lastToken = match self._expectNext() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }
    if lastToken.kind != TokenKind.RBrack {
      return Result.Err(ParseError(position: lastToken.position, kind: ParseErrorKind.UnexpectedToken(lastToken)))
    }

    Result.Ok(AstNode(token: token, kind: AstNodeKind.Indexing(expr: left, index: index)))
  }

  func _parseLambda(self, token: Token, left: AstNode): Result<AstNode, ParseError> {
    val params = match left.kind {
      AstNodeKind.Identifier(identifier) => match identifier {
        IdentifierKind.Named(name) => [FunctionParam(label: Label(name: name, position: left.token.position))]
        _ => return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.UnexpectedToken(token)))
      }
      AstNodeKind.Grouped(inner) => {
        match inner.kind {
          AstNodeKind.Identifier(identifier) => match identifier {
            IdentifierKind.Named(name) => [FunctionParam(label: Label(name: name, position: inner.token.position))]
            _ => return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.UnexpectedToken(token)))
          }
          _ => return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.UnexpectedToken(token)))
        }
      }
      AstNodeKind.Tuple(items) => {
        val params: FunctionParam[] = Array.withCapacity(items.length)
        for item in items {
          match item.kind {
            AstNodeKind.Identifier(identifier) => match identifier {
              IdentifierKind.Named(name) => {
                params.push(FunctionParam(label: Label(name: name, position: item.token.position)))
                0 // <- awkward; this is needed because of an outstanding bug with Unit-returning `match`es (even though this is a statement so it shouldn't matter)
              }
              _ => return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.UnexpectedToken(token)))
            }
            _ => return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.UnexpectedToken(token)))
          }
        }
        params
      }
      _ => return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.UnexpectedToken(token)))
    }

    val body = match self._parseBlockOrSingleExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    return Result.Ok(AstNode(token: token, kind: AstNodeKind.Lambda(LambdaNode(params: params, body: body))))
  }

  func _parseAssignment(self, token: Token, left: AstNode): Result<AstNode, ParseError> {
    val mode = match left.kind {
      AstNodeKind.Identifier(identKind) => match identKind {
        IdentifierKind.Named(name) => AssignmentMode.Variable(name)
        _ => return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.UnexpectedToken(token)))
      }
      AstNodeKind.Accessor(node) => AssignmentMode.Accessor(node)
      AstNodeKind.Indexing(expr, indexMode) => match indexMode {
        IndexingMode.Single(indexExpr) => AssignmentMode.Indexing(expr, indexExpr)
        _ => return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.UnexpectedToken(token)))
      }
      _ => return Result.Err(ParseError(position: token.position, kind: ParseErrorKind.UnexpectedToken(token)))
    }

    val expr = match self._parseExpression() { Result.Ok(v) => v, Result.Err(e) => return Result.Err(e) }

    Result.Ok(AstNode(token: token, kind: AstNodeKind.Assignment(mode: mode, expr: expr)))
  }
}

func todo<V>(position: Position): Result<V, ParseError> {
  Result.Err(ParseError(position: position, kind: ParseErrorKind.NotYetImplemented))
}

func unreachable<V>(): Result<V, ParseError> {
  Result.Err(ParseError(position: Position(line: 0, col: 0), kind: ParseErrorKind.Unreachable))
}

type Precedence {
  // TODO: These are static functions because static constants don't yet exist
  func none(): Int = 0
  func coalesce(): Int = 1
  func orAndAssign(): Int = 2
  func or(): Int = 3
  func and(): Int = 4
  func equality(): Int = 5
  func comparison(): Int = 6
  func addMulAssign(): Int = 7
  func addition(): Int = 8
  func multiplication(): Int = 9
  func unary(): Int = 11
  func call(): Int = 12
  func assignment(): Int = 13

  func forToken(token: Token): Int {
    match token.kind {
      TokenKind.Or => Precedence.or()
      TokenKind.Caret => Precedence.or()
      TokenKind.And => Precedence.and()
      TokenKind.EqEq => Precedence.equality()
      TokenKind.Neq => Precedence.equality()
      TokenKind.LT => Precedence.comparison()
      TokenKind.LTE => Precedence.comparison()
      TokenKind.GT => Precedence.comparison()
      TokenKind.GTE => Precedence.comparison()
      TokenKind.Plus => Precedence.addition()
      TokenKind.Minus => Precedence.addition()
      TokenKind.Star => Precedence.multiplication()
      TokenKind.Slash => Precedence.multiplication()
      TokenKind.Percent => Precedence.multiplication()
      TokenKind.Dot => Precedence.call()
      TokenKind.QuestionDot => Precedence.call()
      TokenKind.Arrow => Precedence.call()
      TokenKind.LParen(sawNewline) => if sawNewline Precedence.none() else Precedence.call()
      TokenKind.LBrack(sawNewline) => if sawNewline Precedence.none() else Precedence.call()
      TokenKind.PlusEq => Precedence.addMulAssign()
      TokenKind.MinusEq => Precedence.addMulAssign()
      TokenKind.StarEq => Precedence.addMulAssign()
      TokenKind.SlashEq => Precedence.addMulAssign()
      TokenKind.PercentEq => Precedence.addMulAssign()
      TokenKind.OrEq => Precedence.orAndAssign()
      TokenKind.AndEq => Precedence.orAndAssign()
      TokenKind.Eq => Precedence.assignment()
      TokenKind.Elvis => Precedence.coalesce()
      TokenKind.ElvisEq => Precedence.coalesce()
      TokenKind.StarStar => Precedence.coalesce()
      _ => Precedence.none()
    }
  }
}
