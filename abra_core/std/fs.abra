import Pointer, Byte from "./_intrinsics"
import "libc" as libc

export enum ReadFileError {
  CouldNotOpen(message: String)
  CouldNotClose(message: String)
  CouldNotSeek(message: String)
  CouldNotRead(message: String)
}

export func readFile(path: String): Result<String, ReadFileError> {
  val fd = libc.open(path._buffer, libc.O_RDONLY)
  if fd == -1 {
    val errMsg = libc.strerror(libc.errno())
    return Err(error: ReadFileError.CouldNotOpen(message: errMsg))
  }

  val len = libc.lseek(fd, 0, libc.SEEK_END)
  if len == -1 {
    val errMsg = libc.strerror(libc.errno())
    return Err(error: ReadFileError.CouldNotSeek(message: errMsg))
  }
  if libc.lseek(fd, 0, libc.SEEK_SET) == -1 {
    val errMsg = libc.strerror(libc.errno())
    return Err(error: ReadFileError.CouldNotSeek(message: errMsg))
  }

  val str = String.withLength(len)
  if libc.read(fd, str._buffer, len) == -1 {
    val errMsg = libc.strerror(libc.errno())
    return Err(error: ReadFileError.CouldNotRead(message: errMsg))
  }
  if libc.close(fd) == -1 {
    val errMsg = libc.strerror(libc.errno())
    return Err(error: ReadFileError.CouldNotClose(message: errMsg))
  }

  Ok(value: str)
}

export func getCurrentWorkingDirectory(): String {
  val buf = Pointer.malloc<Byte>(libc.PATH_MAX)

  val cwd = libc.getcwd(buf, libc.PATH_MAX)
  val len = libc.strlen(cwd._buffer)

  val str = String.withLength(len)
  str._buffer.copyFrom(buf, len)
  str
}
